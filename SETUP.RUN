.Object Name: SETUP.RUN
.REV: 1.0    DATE: 20140415
*===============================================================================
*  SETUP+ Installation Package                                                 *
*  Copyright (c) 2014 - John Thalhuber Associates (JTA)                        *
*  --------------------------------------------------------------------------  *
*  The SETUP+ installation package (program code, table/file structures and    *
*  methodology) may not be reused or distributed, in whole or part, without    *
*  the expressed consent of JTA.  No warranty is provided or implied, and JTA  *
*  will assume no liability for defects or problems resulting from its use.    *
*===============================================================================
@.
@     rer,0,E,2 0001  rar 0196 .
@. @     ldv <*PTH>s256='D:\users\winne\sublwatcher\site-e\admin\setup.run' .
@     call 0001() .
@     if cdrw$ ne 0  esr ; if link$ ne 0,(END) ; rel .
@.
@0001:() .
@     gto lin+3 .
@0002:(<pth>,<deploy>,<cab>) .
@     ldv,pu <*pth>s256=<pth>,<*deploy>h1=<deploy>  ldv <*cab>i4=<cab> .
@. -------------------------- Module Initialization ----------------------------
@.
@. @     rdb,progtested .
@     brk  rnm -8  dlr,-8 .
@.
@     ldv <win1>i6=0 .
@     rer,56,b,0099 0001 .
@.
@     ldv,p <app>s20='sublwatcher         ' .
@     ldv,u <app>(1-1) .
@     ldv <ovr>h1=N .
@     ldv <gen>h1=Y .
@     ldv <ptq>h1=Y .
@     ldv <pth>s256 .
@     ldv,pw <dpt>a4=mstrd$ .
@.
@. ----------------------- Check System & Level Info ---------------------------
@.
@     if <*deploy> eq 'y'  ldv <stp>i1=2  gto 0009 . ; .
@     if ws$ eq 0,(0180) ; .
@     if dwcap$(9-1) ne 1,(0181) ; .
@     if sysnam$(1-1) eq 'M'  if maper$(1-2) lt 37,(0181) ; .
@     if cpriv$ eq 0,(0182) ; .
@     if sysnam$(1-1) eq 'M' . ; gto lin+3 .
@     fdr,usrdrw$,depn$,,,0183 '' 'USER' 	,user$ .
@     rln 46-1 <x>h  if <x> ne '',(0183) ; .
@.
@. --------------------------- Initialize Screen -------------------------------
@.
@     ldv <stp>i1=0 .
@     sc q cs .
@     mnu,t MENUBAR,"",,'' .
@     HID .
@     fon,r .
@     if dwcap$(12-1) eq 0 . ; gto lin+2 .
@     fon,r,'Terminal',11,pd .
@     if dwcap$(12-1) eq 1 . ; gto lin+3 .
@     fon,r,'Terminal',11,p,lin+1  gto lin+2 .
@     fon,r,'MAPPER Standard',11,pd .
@     fon,t .
@     win,,01,01,01,01,,h '' <win1>i6  cls,<win1>  ldv <win1>=0 .
@.
@. -------------------------- Display Wizard Screen ----------------------------
@.
@     ldv,p <b>h1=b,<3>h1  if dwcap$(13-1) eq 1  ldv <b>h1=3,<3>h1=3 ; .
@     ldv <wid>i3=62,<hgt>i2=30 .
@     if scnv$ lt <hgt> . ; gto lin+5 .
@     mbx,OK,E,'Recommendation' "/\
 Please maximize your window to/\
 better utilize this installation wizard." \
      O,(lin+1) .
@     chg <row>i3 scnv$ - <hgt> // 2  if <row> lt 2  ldv <row>=2 ; .
@     chg <col>i3 scnh$ - <wid> // 2 + 1 .
@     win,,<row>,<col>,<hgt>,<wid>,lgra,bc <app>' Setup Wizard' <win1>i6 .
@     txt,"",<win1>,01,01,05,62,bla/whi,b<3> .
@     fon,t,'Arial',09,pdb .
@     txt,"",<win1>,02,04,01,80,bla/whi <msg1>i6 .
@     fon,t,'Arial',09,pd .
@     txt,"",<win1>,04,06,01,80,bla/whi <msg2>i6 .
@     fon,t,'Arial',08,pd .
@     txt,"SETUP+ Installation Tools",<win1>,29,01,02,32,gra/lgra,c .
@     txt,"(c) John Thalhuber Associates",<win1>,30,01,02,32,gra/lgra,c .
@     fon,t,'Arial',08,pdb .
@     btn,<win1>,28,24,02,08 'Read Me' <read>i6  hid,<read> .
@     btn,<win1>,28,34,02,08 '< Back' <back>i6 .
@     btn,<win1>,28,42,02,08 'Next >' <next>i6 .
@     btn,<win1>,28,52,02,08 'Quit'   <exit>i6 .
@     tip," View the README file ",<read> .
@     tip," Go back to the previous step ",<back> .
@     tip," Proceed with the installation process ",<next> .
@     tip," Exit without installing this product ",<exit> .
@.
@     brk,0,A  brk  ln+,-0,1,1 .
@     fon,t,'Arial',09,pd .
@     txt,-0,2,99,<win1>,07,06,16,85,bla/lgra <txth>i6 .
@     fon,t .
@.
@0009:if <*PTH> ne '<*PTH>'  ldv,p <ptx>s256=<*PTH>  gto 0024 ; .
@.
@0010:. ------------------------ Welcome Screen --------------------------------
@.
@     ldv <stp>i1=1 .
@.
@     txt,"",<win1>,01,01,25,62,bla/whi,<b> <txt1>i6 .
@     txt,"",<win1>,01,01,25,20,bla <txt2>i6 .
@     ldv <pic1>i6=0 .
@     if <ptx> ne '<PTX>'  if <ptx> gt ' ' . ; gto lin+2 .
@     lzr,-0 .
@     fon,t,'Arial',14,pd .
@     brk,0,A .
 Welcome to the <app>
 Package Manager
@     brk  txt,-0,2,2,<win1>,03,23,02,40,bla/whi <txt3>i6 .
@     fon,t,'Arial',09,pd .
@     brk,0,A ._
 This wizard will guide you through the steps
 required to install <app> on your system.
 To continue, click Next.
@     brk  txt,-0,2,99,<win1>,08,23,09,62,bla/whi <txt4>i6 .
@     fon,t .
@.
@     if <pth> ne ''  shw,<read> . ; hid,<read> .
@     btn,<back>,,,,,,g '< Back' .
@     btn,<next> 'Next >' .
@     ldv <foc>i6=<next> .
@0011:.
@     inp,<foc>,,<win1> .
@     if actinp$ eq <next>,(0012) ; .
@     if actinp$ eq <read>  pc,bcw "START <pth>\readme.txt" ; .
@     if actinp$ eq <exit>,(0198) ; .
@     gto 0011 .
@0012:.
@     cls,<pic1>,<txt1>,<txt2>,<txt3>,<txt4> .
@.
@     if <ptq> eq 'N',(0040) ; .
@.
@0020:. ------------------- Specify Installation Path --------------------------
@.
@     ldv <stp>i1=2 .
@.
@     ldv,p <ptx>s256=<pth>  if <ptx> eq ''  ldv,p <ptx>s3='D:\' ; .
@     txt,"Installation Path",<msg1> .
@     txt,"Where are you loading the product from?",<msg2> .
@.
@     brk,0,A .
 You must specify the drive or directory containing
 the <app> product software ( i.e., D:\ ).
 To continue, click Next.
@     brk  txt,-0,2,99,<txth> .
@.
@     txt,"",<win1>,14,08,07,47,lgra,<b> <txt1>i6 .
@     fon,t,'Arial',08,pd .
@     txt,"Installation Media Path",<win1>,16,11,01,24,bla/lgra <txt2>i6 .
@     fon,t .
@     edt,"<ptx>",<win1>,17,11,01,41,bla/whi,b<3>h <edt1>i6 .
@.
@     btn,<back> '< Back' .
@     btn,<next> 'Next >' .
@     ldv <foc>i6=<edt1> .
@0021:.
@     inp,<foc>,,<win1> .
@     if actinp$ eq <next>,(0023) ; .
@     if actinp$ eq <edt1>,(0023) ; .
@     if actinp$ eq <back>,(0022) ; .
@     if actinp$ eq <exit>,(0198) ; .
@     gto 0021 .
@0022:.
@     cls,<txt1>,<txt2>,<edt1> .
@     gto 0010 .
@0023:.
@     inp,<edt1>,Y .
@     chg input$ <ptx>s256  ldv,p <ptx> .
@0024:.
@     def,s <siz>i3,<ptx>  if <siz> ge 9 . ; gto lin+2 .
@     if <ptx>(0-9) eq 'SETUP.RUN'  ldv <ptx>(0-9)=''  ldv,p <ptx> ; .
@     if <ptx> ne ''  if <ptx>(0-1) eq '\'  ldv <ptx>(0-1)=''  ldv,p <ptx> ; .
@     if <ptx> eq '',(0184) ; .
@     ldv <x>h7='/*?"<>|'  DEF,s <n>i3,<x> .
@     lcv,0184 n <ptx> <x>(<n>-1)  dec <n>  if <n> gt 0,(lin-0) ; .
@     def,s <siz>i3,<ptx>  if <siz> gt 1,(lin+2) ; .
@     if <ptx> ge 'A' & le 'Z'  ldv <drv>h1=<ptx>,<ptx>h2=<drv>':' ; .
@     if <*deploy> eq 'Y' \
        ret,0,a,n,n,0185 <ptx>'\'SETUP.INF . ; \
        pcr,0,f,-0,,,0185 <ptx>'\'SETUP.INF .
@     rnm -2 .
@     ln+,-2,1,1  wrl,-2,2 2-1 *,= .
@     srh,-2,,,0185 dp 2-8 [,DRAWERS] .
@     srh,-0,,,0185 dp 2-6 ,Drawer <dcnt>i2 .
@     rnm -6 .
@     rsl,-2  call 0170 ('CONFIG','IBldApp',<ibldapp>h4) .
@     rsl,-2  call 0170 ('CONFIG','IceEnabled',<iceapp>h4) .
@     rsl,-2  call 0170 ('CONFIG','AppCab',<appcab>i4) .
@     ldv,p <ibldapp>,<iceapp> .
@     if <drwa> eq '<DRWA>'  lda <drwa>h1[<dcnt>]  ldv <cnt>i2=0 . ; .
@     loc,-6,4,0185 afm 2-79 Drawer ,<lin>i4  dec,2 <lin> .
@0025:inc <lin>,<cnt>  rdl,-6,<lin>,0026 1-80 <dat>s .
@     lcv,0185 '' <dat> '=' <col>i2  inc <col> .
@     ldv,lq <ddrw>h1=<dat>(<col>-0),0(,),\
             <cpl>i3=<dat>(<col>-0),1(,),\
             <dnm>s26=<dat>(<col>-0),2(,) .
@     lda <drwa>[<cnt>]=<ddrw> .
@     if <ddrw> eq 'G'  gto 0025 . ; .
@.
@.    #  ICE BUILD Applications
@     if <ibldapp> eq y . ; gto 0125 .
@     if <ddrw> eq 'B'  ldv,p <appfolder>s20='db',<subfolder>s20='tables' . ; .
@     if <ddrw> eq 'C'  ldv,p <appfolder>s20='lib',<subfolder>s20='internal' . ; .
@     if <ddrw> eq 'D'  ldv,p <appfolder>s20='lib',<subfolder>s20='tables' . ; .
@     if <ddrw> eq 'E'  ldv,p <appfolder>s20='db',<subfolder>s20='schema' . ; .
@     if <ddrw> eq 'F'  ldv,p <appfolder>s20='app',<subfolder>s20='controllers' . ; .
@     if <ddrw> eq 'H'  ldv,p <appfolder>s20='app',<subfolder>s20='views' . ; .
@     if <ddrw> eq 'I'  ldv,p <appfolder>s20='app',<subfolder>s20='models' . ; .
@     if <*deploy> eq 'y' \
        ret,0,f,n,n,0185 <ptx>'\'<appfolder>'\'<subfolder>'\TOC.TXT' . ; \
        pcr,0,F,-0,,,0185 <ptx>'\'<appfolder>'\'<subfolder>'\TOC.TXT' .
@     gto 0025 .
@.
@.    #  Standard applications
@0125:if <*deploy> eq 'y' \
        ret,0,f,n,n,0185 <ptx>'\'<dnm>(p)'\TOC.TXT' . . ; \
        pcr,0,f,-0,,,0185 <ptx>'\'<dnm>(p)'\TOC.TXT' .
@     gto 0025 .
@0026:.
@     if <iceapp> eq 'N' gto 0126 . ; .
@     if <*deploy> eq 'y' \
        ret,0,f,n,n,0178 <ptx>'\SVCTBL.TXT' \
        ret,0,f,n,n,0179 <ptx>'\CTGTBL.TXT' . ; \
        pcr,0,F,-0,,,0178 <ptx>'\SVCTBL.TXT' \
        pcr,0,F,-0,,,0179 <ptx>'\CTGTBL.TXT' .
@0126:ldv,p <pth>s256=<ptx> .
@     brk,0,A .
.
*
* Run Name   .Rpt .I/O  .LLP   1MDNF.RDRW.
*============.====.=====.=====.=====.====.
@     srh,-2,,,0028 dp 2-8 [,RUNS]  ldv <lin>i6=3 .
@0027:inc <lin>  loc,-0,<lin>,0028 afm 2-79  run ,<lin> .
@     rdl,-0,<lin> 1-80 <dat>s  lcv,0185 '' <dat> '=' <col>i2  inc <col> .
@     ldv,lq <run>h12=<dat>(<col>-0),0(,) .
@     ldv,rq <rpt>i4=<dat>(<col>-0),1(,) .
@     ldv,rq <ios>i5=<dat>(<col>-0),2(,) .
@     ldv,rq <llp>i5=<dat>(<col>-0),3(,) .
@     ldv,lq <flg>h5=<dat>(<col>-0),4(,) .
@     ldv,lq <rdrw>h4=<dat>(<col>-0),5(,) .
	<run>	<rpt>	<ios>	<llp>	<flg>	<rdrw>	
@     gto 0027 .
@     brk  rnm -4 .
@0028:.
@     brk,0,A .
.
*     d.    .    .
*Cab  r.BRpt.ERpt.
*====.=.====.====.
@     srh,-2,,,0129 dp 2-8 [,DATABASES]  ldv <lin>i6=3 .
@0128:inc <lin>  loc,-0,<lin>,0129 afm 2-79 Location ,<lin> .
@     rdl,-0,<lin>,0129 1-80 <dat>s  lcv,0185 '' <dat> '=' <col>i2  inc <col> .
@     ldv,lq <dbrpts>h11=<dat>(<col>-0),0(,),\
             <dbdrw>h1=<dat>(<col>-0),1(,),\
             <dbcab>i4=<dat>(<col>-0),2(,),\
             <dbdesc>s40=<dat>(<col>-0),3(,) .
@     lcv,lin1 'b1' <dbrpts> '-' <dpos>i3  ldv <mult>h1='Y'  gto lin+2 .
@     ldv <mult>h1='N' .
@     if <mult> eq 'N' \
        ldv <begrpt>i4=<dbrpts>,<endrpt>i4=<dbrpts> . ; \
        dec <dpos>    ldv <begrpt>i4=<dbrpts>(1-<dpos>) \
        inc,2 <dpos>  ldv <endrpt>i4=<dbrpts>(<dpos>-0) .
	<dbcab>(r)	<dbdrw>	<begrpt>	<endrpt>	
@     gto 0128 .
@0129:brk  rnm -7 .
@     if <stp> eq 2,(0029) ; .
@     ldv <ptq>h1=N .
@     gto 0010 .
@0029:.
@.
@     if <*deploy> eq 'y' . ; cls,<txt1>,<txt2>,<edt1> .
@.
@0040:. ------------------- Ice Registration Info ------------------------------
@.
@     if <iceapp> eq 'N'  ldv <cab>i4=<appcab>  gto lin+3 . ; .
@     fdr,3950,d,1,,,0044 '' 'Category' 	,<app>  rln 'SvcCab' <cab>i .
@     ldv,p <cab> .
@.
@     if <*deploy> eq 'Y' gto 0043 . ; .
@     mbx,YN,Q,'Confirmation' "/\
 A prior installation was detected.//\
 Application <app> is already/\
 installed in Cabinet <cab>.//\
 Do you want to overlay the existing version?" \
      N,(0044) .
@0043:ldv <ovr>h1=Y,<gen>h1=N .
@     gto 0045 .
@0044:.
@     ldv <ovr>h1=N,<gen>h1=Y .
@0045:.
@.
@     if <*deploy> eq 'Y'  ldv <cab>i4=<*cab>  gto 0080 . ; .
@     if <ovr> eq 'Y',(0070) ; .
@.
@0050:. -------------------- Specify Empty Drawer ------------------------------
@.
@     ldv <stp>i1=5 .
@.
@     ldv <gen>h1=Y .
@     ldv <cab>i4=200,<drw>h1=B  if sysnam$(1-1) eq 'M'  ldv <cab>=2 ; .
@     def,q <lgth>i2,<drwa>  ldv <cnt>i2=0 .
@0051:inc <cnt>  if <cnt> gt <lgth>,(0052) .
@     ldv <chkdrw>h1=<drwa>[<cnt>] .
@     drw,<cab>,<chkdrw>,0051  ldv <cnt>i2=0 .
@     inc,2 <cab>  if <cab> le maxcab$,(0051) ; gto 0187 .
@0052:ldv,p <cab> .
@     txt,"Cabinet Location",<msg1> .
@     txt,"Designate cabinet to contain <app>.",<msg2> .
@     brk,0,A .
 Specify an empty cabinet to load the <app> package into.
 The first available cabinet on your system is: <cab>.
@     brk  txt,-0,2,99,<txth> .
@.
@     txt,"",<win1>,13,15,07,34,lgra,<b> <txt1>i6 .
@     fon,t,'Arial',08,pd .
@     txt,"Cabinet",<win1>,15,27,01,12,bla/lgra <txt2>i6 .
@     fon,t .
@     edt,"<cab>",<win1>,16,27,01,04,bla/whi,b<3>n <edt1>i6 .
@.
@     btn,<back> '< Back' .
@     btn,<next> 'Next >' .
@     ldv <foc>i6=<edt1> .
@0053:.
@     inp,<foc>,,<win1> .
@     if actinp$ eq <next>,(0055) ; .
@     if actinp$ eq <edt1>,(0055) ; .
@     if actinp$ eq <back>,(0054) ; .
@     if actinp$ eq <read>  pc,bcw "START <pth>\readme.md" ; .
@     if actinp$ eq <exit>,(0198) ; .
@     gto 0053 .
@0054:.
@     cls,<txt1>,<txt2>,<edt1> .
@0055:chg input$ <cab>a4  ldv,p <cab> .
@     def <n>i1,<cab>  if <n> ne 1,(0188) ; .
@     lcv,0188 n <cab> '-'  lcv,0188 n <cab> '+'  lcv,0188 n <cab> '.' .
@     ldv,r <cax>a4=<cab>  if <cax>(0-1) eq 1,3,5,7,9,(0188) ; .
@     if sysnam$(1-1) eq 'M' . ; if <cab> ge 16 & le maxcab$ . ; gto 0189 .
@     if sysnam$(1-1) ne 'M' . ; if <cab> ge 2 & le maxcab$ . ; gto 0190 .
@     if sysnam$(1-1) ne 'M' . ; if <cab> ge 200 & le 250,(0191) ; .
@.
@     mbx,YN,Q,'Confirmation' "/\
 The drawer you specified already exists.//\
 Are you sure you want to overlay the existing/\
 reports in Cabinet <cab> Drawer <drw>?" \
      N,(0053) .
@0056:.
@     ldv <gen>h1=N .
@0057:.
@     cls,<txt1>,<txt2>,<edt1> .
@.
@     if sysnam$(1-1) eq 'M'  if <gen> eq 'Y',(0060) ; gto 0070 .
@.
@0070:. ---------------------- Final Verification ------------------------------
@.
@     ldv <stp>i1=7 .
@.
@     txt,"Installation Confirmation",<msg1> .
@     txt,"Is this information correct?",<msg2> .
@.
@     brk,0,A .
 Examine the following installation information carefully.
 Click Back if you need to modify options or selections.
 If correct, click Install to perform the installation.
 [Destination]
@     if <gen> eq 'N' . ; gto 0071 .
@     srh,-2,,,0185 dp 2-8 [,DRAWERS] <dcnt>i2 .
@     srh,-0,,,0185 dp 2-6 ,Drawer <dcnt>i2 .
@     loc,-0,4,0185 afm 2-79 Drawer ,<lin>i4  dec,2 <lin> .
@     inc <lin>,<cnt>  rdl,-0,<lin>,0073 1-80 <dat>s .
@     lcv,0185 '' <dat> '=' <col>i2  inc <col> .
@     ldv,lq <ddrw>h1=<dat>(<col>-0),0(,) .
  Drawer <ddrw><cab> - Existing drawer will be overwritten
@     gto lin-4 .
@0071:if <gen> eq 'Y' . ; gto 0072 .
@     srh,-2,,,0185 dp 2-8 [,DRAWERS] .
@     srh,-0,,,0185 dp 2-6 ,Drawer <dcnt>i2 .
@     loc,-0,4,0185 afm 2-79 Drawer ,<lin>i4  dec,2 <lin> .
@     inc <lin>,<cnt>  rdl,-0,<lin>,0073 1-80 <dat>s .
@     lcv,0185 '' <dat> '=' <col>i2  inc <col> .
@     ldv,lq <ddrw>h1=<dat>(<col>-0),0(,) .
  Drawer <ddrw><cab> - New drawer will be generated
@     gto lin-4 .
@0072:if <gen> eq 'Y'  if sysnam$(1-1) eq 'M' . ; gto 0073 .
  Database file <fil> will be used
@0073:lzr,-4 <lns>i4  if <lns> lt 6,(0074) ; .
 [Run Registration]
@     ldv,w <usr>h12=user$  if <all> eq 1  ldv <usr>=All ; .
  Department: <dpt>    User(s): <usr>
@0074:brk  txt,-0,2,99,<txth> .
@.
@     btn,<back> '< Back' .
@     btn,<next> 'Install' .
@     ldv <foc>i6=<next> .
@0075:.
@     inp,<foc>,,<win1> .
@     if actinp$ eq <next>,(0077) ; .
@     if actinp$ eq <back>,(0076) ; .
@     if actinp$ eq <read>  pc,bcw "START <pth>\readme.txt" ; .
@     if actinp$ eq <exit>,(0198) ; .
@     gto 0075 .
@0076:.
@     if sysnam$(1-1) eq 'M'  if <gen> eq 'Y',(0060) ; .
@     if <ovr> eq 'N',(0050) ; .
@     lzr,-4 <lns>i4  if <lns> lt 6,(0050) ; gto 0040 .
@0077:.
@     cls,<read>,<back>,<next>,<exit> .
@     ldv <stp>i1=0 .
@.
@0080:. ------------------------ Drawer Setup ----------------------------------
@.
@. Get List of Drawers
@.
@.    #  Will contain a list of services that couldn't be found...
@.    #  Somethine will show up in this list if it's in the TOC.TXT but
@.    #  hasnt been commited to version control yet.
@     brk .
*    .D.    .   .                    .
*Cab .w.Rpt .Typ.Name                .
*====.=.====.===.====================.
@     brk rnm -8 .
@.
@     lzr,-6 ,,<dlin>i4 .
@0081:inc <dlin> .
@     rdl,-6,<dlin>,0098 1-80 <dat>s  lcv,0185 '' <dat> '=' <col>i2  inc <col> .
@     ldv,lq <drw>h1=<dat>(<col>-0),0(,),\
             <cpl>i3=<dat>(<col>-0),1(,),\
             <dnm>s26=<dat>(<col>-0),2(,) .
@.
@     drw,<cab>,<drw>,lin1 <chr>i3  gto 0082 .
@.
@.    if <chr> ne <cpl> ...
@. TODO if sizes are different, we should REGEN the drawer...
@.
@. Generate Drawer
@.
@     if <*deploy> eq 'Y'  gto 0085 . ; .
@     txt,"Drawer Generation",<msg1> .
@     txt,"... Please Wait ...",<msg2> .
@     brk,0,A .
 Drawer: <dnm>(p)  <drw><cab> is now being generated ...
@     brk  txt,-0,2,99,<txth>,,,,,,s .
@.
@     wat 1000 .
@.
@0085:if sysnam$(1-1) eq 'M' . ; gto lin+2 .
@     gen,<cab>,<drw> <fil>,<cpl>,F,,,,<ttl>(1-24),user$ .
@     if sysnam$(1-1) ne 'M' . ; gto lin+2 .
@     gen,<cab>,<drw>,<cpl> .
@.
@0082:. Update Drawer Permission Report
@.
@     if <*deploy> eq 'Y'  gto 0086 . ; .
@     txt,"Drawer Registration: <dnm>(p)",<msg1> .
@     txt,"... Please Wait ...",<msg2> .
@     brk,0,A .
 Updating Drawer Permissions report ...
@     brk  txt,-0,2,99,<txth>,,,,,,s .
@.
@     wat 1000 .
@.
@0086:if 0 eq secdrw$,secrpt$,secgrp$,(0084) ; .
@     DFU secdrw$,secrpt$ .
@     ldv <lin>i6=5 .
@     inc <lin>  rdl,secdrw$,secrpt$,<lin>,0083 1-1,29-9 <lt>h,<dc>a .
@     if <lt> ne 	,(lin-1) ; .
@     ldv,l <dc>  if <dc>(2-0) eq <cab>  if <dc>(1-1) eq <drw> . ; gto lin-2 .
@     ln-,secdrw$,secrpt$,<lin>,1 .
@0083:.
@     ldv,r <drc>h6=<drw><cab>(p) .
@     if sysnam$(1-1) eq 'M' \
      ldv,rz <zcb>a4=<cab>  ldv <drc>h6=<drw>' '<zcb> ; .
@     ldv <sor>i1=0 .
@     bfn,secdrw$,secrpt$,,lin+1 k 2-26 	,k  ldv <sor>=1  gto lin+2 .
@     bfn,secdrw$,secrpt$,,lin+1 kf'2,1' 34-4,32-1 	,k,k  ldv <sor>=2 .
@     fdr,secdrw$,secrpt$ '' 2-1 *,= ,<lin>i6 .
@     ln+,secdrw$,secrpt$,<lin>,1  inc <lin> .
@     chg <clm>i3 56 + secgrp$ .
@     wrl,secdrw$,secrpt$,<lin> 2-26,32-6,<clm>-1 	,<dnm>,<drc>,W .
@     wrl,secdrw$,secrpt$,<lin> 28-1,38-1,56-1 	,	,	,	 .
@     if <sor> eq 1  srr,secdrw$,secrpt$ '' 2-26,29-9 	,1,2 ; .
@     if <sor> eq 2  srr,secdrw$,secrpt$ '' 34-4,32-1 	,2n,1 ; .
@     srr,secdrw$,secrpt$ '' 2-26,29-9 	,1,2 ; .
@     cmu .
@.
@0084:. Update Drawer Description
@.
@     if <*deploy> eq 'Y'  gto 0087 . ; .
@     txt,"Drawer Description: <dnm>(p)",<msg1> .
@     txt,"... Please Wait ...",<msg2> .
@     brk,0,A .
 Updating Cabinet Table of Contents report ...
@     brk  txt,-0,2,99,<txth>,,,,,,s .
@.
@     wat 1000 .
@.
@0087:ldv <row>i1=1,<col>i2=4 .
@     if sysnam$(1-1) ne 'M'  if <drw> eq 'F','G','H','I'  ldv <col>=39 ; .
@     if sysnam$(1-1) eq 'M'  if <drw> eq 'F','G','H','I'  ldv <col>=38 ; .
@     if <drw> eq 'C','G'  ldv <row>=2 ; .
@     if <drw> eq 'D','H'  ldv <row>=3 ; .
@     if <drw> eq 'E','I'  ldv <row>=4 ; .
@     if sysnam$(1-1) ne 'M'  chg <lin>i6 <cab> // 2 * 6 + 18 + <row> ; .
@     if sysnam$(1-1) eq 'M'  chg <lin>i6 <cab> // 2 * 15 + 21 + <row> ; .
@     lok,typdrw$,typrd$ .
@     ldv <siz>i2=26  if sysnam$(1-1) eq 'M'  ldv <siz>=24 ; .
@     wrl,typdrw$,typrd$,<lin> <col>-<siz> ' ',<dnm>(1-<siz>) .
@     ulk .
@.
@0090:. ---------------------- Load Runs & Data --------------------------------
@.
@.    #  ICE BUILD Applications
@     if <drw> eq 'G'  gto 0081 .
@     lcv,lin1 '' <dnm> 'dumps'  gto 0081 . ; .                Skip error dumps
@     if <ibldapp> eq y . ; gto 0105 .
@     if <drw> eq 'B'  ldv,p <appfolder>s20='db',<subfolder>s20='tables' . ; .
@     if <drw> eq 'C'  ldv,p <appfolder>s20='lib',<subfolder>s20='internal' . ; .
@     if <drw> eq 'D'  ldv,p <appfolder>s20='lib',<subfolder>s20='tables' . ; .
@     if <drw> eq 'E'  ldv,p <appfolder>s20='db',<subfolder>s20='schema' . ; .
@     if <drw> eq 'F'  ldv,p <appfolder>s20='app',<subfolder>s20='controllers' . ; .
@     if <drw> eq 'H'  ldv,p <appfolder>s20='app',<subfolder>s20='views' . ; .
@     if <drw> eq 'I'  ldv,p <appfolder>s20='app',<subfolder>s20='models' . ; .
@     if <*deploy> eq 'y' \
        ret,0,f,n,n,0185 <ptx>'\'<appfolder>'\'<subfolder>'\TOC.TXT' . ; \
        pcr,0,f,-0,,,0185 <ptx>'\'<appfolder>'\'<subfolder>'\TOC.TXT' .
@     rnm -3 .
@     gto 0106 .
@.
@.    #  Standard applications
@0105:if <*deploy> eq 'y' \
        ret,0,f,n,n,0185 <ptx>'\'<dnm>(p)'\TOC.TXT' . ; \
        pcr,0,f,-0,,,0185 <ptx>'\'<dnm>(p)'\TOC.TXT' .
@     rnm -3 .
@.
@0106:. tot,-3 '' 'Lines' '	',+ <tln>i12 .
@.
@     if <*deploy> eq 'Y' gto 0109 . ; .
@     txt,"Software Modules",<msg1> .
@     txt,"... Please Wait ...",<msg2> .
@     brk,0,A .
 Loading <app> code modules, tables and other reports ...
@     brk  txt,-0,2,99,<txth>,,,,,,s .
@.
@     txt,"",<win1>,15,13,08,37,lgra,<b> <txt1>i6 .
@     fon,t,'Arial',08,pd .
@     txt,"Completion Status",<win1>,14,14,01,18,bla/lgra,c <txt2>i6 .
@     txt,"",<win1>,21,24,01,24,bla/lgra,c <txt3>i6 .
@     fon,t .
@     txt,"",<win1>,17,19,03,25,whi,<b> <txt4>i6 .
@     txt,"",<win1>,17,19,03,25,whi,<b> <txt5>i6 .
@     txt,"",<win1>,18,29,01,05,dblu/whi,cs <txt6>i6 .
@     juv,r <cab> .
@.
@0109:.
@     lzr,-3 ,,<rlin>i4 .
@0091:inc <rlin>  rdl,-3,<rlin>,0097 'Cab','Dw','Rpt','Typ','Name','Title' \
        <tcab>i,<tdrw>h,<rpt>a,<ext>h,<svcnme>s,<ttl>s .
@.
@. @     inc <qty>  inc,<rln> <xln>  juv,r <rpt>  ldv,l <rid>h5=<rpt><drw> .
@     juv,r <rpt>  ldv,l <rid>h5=<rpt><drw> .
@     if <*deploy> eq 'Y' . ; txt,"Loading Report: <rid>",<txt3> .
@     ldv,pu <report>s20=<rpt>(p)<tdrw>(p)<tcab>(p)'.'<ext>(p) .
@     ldv <bin>h1=N .
@     if <ext> eq 'BMP','PCX','JPG','BIN','EXE','PNG'  ldv <bin>h1=Y ; .
@     if <bin> eq Y  ldv,p <report>s79=<ttl> .
@.
@     if <svcnme> eq '' \
        ldv,p <svc>s40=<report> . ; \
        ldv,p <svc>s40='('<svcnme>(p)')'<report> .
@     if <bin> eq 'Y'  ldv,p <svc>s40=<report> . ; .
@.
@.    #  Database rids (Replace if new otherwise don't touch again)
*     d.    .    .
*Cab  r.BRpt.ERpt.
*====.=.====.====.
@     fdr,-7,,,0092 '' 'Cab','Dr','BRpt' 	,<tcab>,<tdrw>,<rpt> .
@     lzr,<tcab>,<tdrw>,<rpt>,0092   gto 0091 .
@. @     pcr,<cab>,<drw>,-0,<bin> <pth>'\'<dnm>(p)'\'<svcnme>(p)<report> .
@. @     rnm -8  lzr,-8 ,<cc>i3,<hln>i3  dec <hln> .
@. @     brk,0,I  rdc,-8,2,<hln> 1-<cc> <hdgs>s .
@. <hdgs>
@. @     brk  rnm -8 .
@. @     brk,0,I  rdc,<tcab>,<tdrw>,<rpt>,2,<hln> 1-<tcc> <hdgs>s .
@. <hdgs>
@. @     brk .
@. @     dif,-0,-8 ' ' .
@. @     lzr,-0 <lns>i7,,<hln>i3  ldv <dif>h1=N  if <lns> eq <hln>,(0092) .
@. @     inc,4 <hln>  loc,-0,<hln> afm 2-79 DATE ,<lin>I4  inc <lin> .
@. @     loc,-0,<lin>,lin+1 afm 2-2 '>+'  ldv <dif>=Y .
@. @     loc,-0,<lin>,lin+1 afm 2-2 '<-'  ldv <dif>=Y .
@. @.
@. @.    #  Replace headings if new
@. @     if <dif> eq Y . ; gto 0092 .
@. @     lok,<tcab>,<tdrw>,<rpt> .
@. @     add,<tcab>,<tdrw>,<rpt>,-8 .
@. @     rep,<tcab>,<tdrw>,<rpt> .
@. @     ulk .
@.
@0092:if <ibldapp> eq 'y' . ; gto 0107 .
@     if <drw> eq 'H'  if <rpt> gt 999  \
        ldv,p <tpth>s998=<pth>'\'<appfolder>'\'<subfolder>'\shared' . ; \
        ldv,p <tpth>s998=<pth>'\'<appfolder>'\'<subfolder> .
@     if <*deploy> eq 'Y' \
        ret,<cab>,<drw>,<bin>,n,lin1 <tpth>'\'<svc>  gto 0108 . ; \
        pcr,<cab>,<drw>,-0,<bin>,,lin1 <tpth>'\'<svc>  gto 0108 .
@     brk .
	<tcab>	<tdrw>	<rpt>	<ext>	<svcnme>	
@     brk  add,-0,-8  rnm -8 .
@     gto 0091 .
@.
@0107:if <*deploy> eq 'Y' \
        ret,<cab>,<drw>,<bin>,n,lin1 <pth>'\'<dnm>(p)'\'<svc>(p)  gto 0108 . ; \
        pcr,<cab>,<drw>,-0,<bin>,,lin1 <pth>'\'<dnm>(p)'\'<svc>(p)  gto 0108 .
@     brk .
	<tcab>	<tdrw>	<rpt>	<ext>	<svcnme>	
@     brk  add,-0,-8  rnm -8 .
@     gto 0091 .
@.
@.    #  Handle character replacement
@0108:if <rpt> eq 0,(0093) .
@     if <bin> eq y,(0093) .
@.
@.    #  Check if we need to replace this file.  This is here to help performance when
@.    #  replacing files in an entire application.
@.    #  Will read the time stamps from the TOC.TXT files and then reference them agains the local rpt
@.    #  If the TOC.TXT timestamps are newer than the local files we'll replace ther report.
@. @     lzr,<cab>,<drw>,<rpt>,0110 .
@. @     rdl,<cab>,<drw>,<rpt>,1,0036 9-9,20-8,42-9,53-11 <lupddt>h,<lupdtm>h,\
@.         <lrepdt>h,<luserpt>h .
@. @     if <lupddt>  EQ ''  ldv <lupddt>=<lrepdt> ; .
@. @     if <upddate> EQ ''  ldv <upddate>=<repdate> ; .
@. @     if <luserpt> ne USER$  gto 0110 . ; .
@. @     if,d2 <upddate> GT <lupddt>  gto 0110 . ; .
@. @     if,d2 <upddate> EQ <lupddt>  if <upddate> GT <lupdtm>  gto 0110 . ; .
@. @     gto 0091 .
@.
@0110:chg <ns>h1 chr$254  lzr,-0 ,<cc>i4 .
@     chg <ns1>h1 chr$302 .
@     ldv <char>h0 .
@     lch,-0,,lin+1 'af' 1-<cc> <ns1><ns>/tab$ .
@     lch,-0,,lin+1 'af' 1-<cc> <ns1>/'' .
@     lch,-0,,lin+1 'af' 1-<cc> <ns>/tab$ .
@     rep,<cab>,<drw>,<rpt> .
@.
@0093:. chg <pc>i4 <xln> / <tln> * 100  chg <wid>i4 <pc> * 25 / 100 .
@     if <*deploy> eq 'Y'  gto 0091 . ; .
@. @     if <wid> gt 0  txt,"",<txt5>,17,19,03,<wid>,dblu ; .
@. @     ldv,p <pc>  txt,"<pc>%",<txt6>,,,,,,s .
@     if <rpt> eq 0,(0094) ; .
@.ew  lok,<cab>,<drw>,<rpt>  wrl,<cab>,<drw>,<rpt>,2 2-79 .,<ttl>  ulk .
@     gto 0091 .
@0094:.
@     rnm -5 .
@     drw,<cab>,<drw> <cpl>i3 .
@     lok,<cab>,<drw>,0 .
@     ldv <zln>i6=1 .
@0095:inc <zln>  rdl,-5,<zln>,0096 1-<cpl> <dat>s .
@     chg <wid>i3 <cpl> - 1 .
@     if <dat>(1-1) eq '.'  if <wid> gt 79  ldv <wid>=79 ; .
@     wrz,<cab>,<drw>,<zln> 2-<wid> <dat>(1-1),<dat>(2-<wid>) .
@     gto 0095 .
@0096:.
@     ulk .
@     gto 0091 .
@0097:.
@     gto 0081 .                                        Get next drawer and runs
@. @0098:cls,<txt1>,<txt2>,<txt3>,<txt4>,<txt5>,<txt6> .
@0098:if <*deploy> eq 'Y'  gto 0100 . ; .
@     cls,<txt1>,<txt2>,<txt3>,<txt4> .
@.
@0099:. -------------------- ICE Registration ---------------------------
@.
@     if <iceapp> eq 'N'  gto 0150 . ; .
@     txt,"ICE Registration",<msg1> .
@     txt,"... Please Wait ...",<msg2> .
@     brk,0,A .
 Registering ice categorie(s) and services(s) ...
@     brk  txt,-0,2,99,<txth>,,,,,,s .
@.
@     wat 1000 .
@.
@0100:if <iceapp> eq 'N' gto 0150 . ; .
@     if <*deploy> eq 'y' \
        ret,0,f,n,n,0178 <ptx>'\CTGTBL.TXT' . ; \
        pcr,0,F,-0,,,0178 <ptx>'\CTGTBL.TXT' .
@     rnm -4 .
@     if <*deploy> eq 'y' \
        ret,0,f,n,n,0179 <ptx>'\SVCTBL.TXT' . ; \
        pcr,0,F,-0,,,0179 <ptx>'\SVCTBL.TXT' .
@     rnm -5 .
@.
@     fdr,3950,d,1,,,lin+1 '' 'Category' 	,<app>  gto 0101 .
@.
@.    #  NEW!
@.    #  Place new services report in D3950
@     dup,-5,3950,D  ldv <srpt>i4=stat1$ .
@     dfu 3950,d,1 .
@.
@.    #  Add category to 1D3950 updating location of services report
@     fdr,-4 '' 'Category' 	,<app> ,<fln>i3 .
@     wrl,-4,<fln>,y 'Rpt' 	,<srpt> .
@     add,-4,3950,d,1 .
@     sor,-0 '' 'Category' 	,1 .
@     rep,3950,d,1 .
@.
@     gto 0103 .
@.
@.    #  UPDATE!
@0101:rln 'Rpt','SvcCab' <srpt>i,<scab>i .
@     dfu 3950,d,1,3950,d,<srpt> .
@.
@.    #  Replace services table with new
@     rep,-5,3950,d,<srpt> .
@.
@.    #  Update svc cab if different
@     if <scab> ne <cab> . ; gto 0103
@     fdr,3950,d,1 '' 'Category' 	,<app> ,<fln>i3 .
@     wrl,3950,d,1,<fln>,y 'SvcCab' 	,<cab> .
@.
@0103:cmu .
@     gto 0150 .
@.
@0150:. ------------------------- Install Complete -----------------------------
@.
@.    #===   Build DEF Table   =================================================
@     juv,d <cab> .
@     if <cab> eq 100,56  gto lin+3 .
@     call,"BLD-DEFTABLE" 0001 (<cab>,' ','1','XALL','ALL') .
@     juv,d <cab> .
@.
@.    #  Update version on DEFLIST_
@     dvs,200,b,4 'Version' <ver>h .
@     rsl,-2  call 0170 ('REVISION','Level',<ver>) .
@     ldv,l <ver> .
@     lok,200,b,4 .
@     fdr,200,b,4 '' 'Cab' 	,<cab> ,<fln>i3 .
@     wrl,200,b,4,<fln>,y 'Version' 	,<ver> .
@     ulk .
@.
@     ldv <stp>i1=8 .
@.
@     if <*deploy> eq 'Y'  gto 0152 . ; .
@     txt,"Installation Complete",<msg1> .
@     txt,"Press Finish to display Installation Notes",<msg2> .
@.
@     brk,0,A .
  <app> successfully loaded into Cabinet <cab>.
@     lzr,-4 <lns>i4  if <lns> lt 6,(lin+2) ; .
  Run script(s) were registered in Department <dpt>.
@     brk  txt,-0,2,99,<txth>,,,,,,s .
@.
@     fon,t,'Courier New',08,pd  .
@     brk,0,A .
@     ldv <clr>h3=whi  if dwcap$(13-1) eq 1  ldv <clr>h6=E8E8E8 ; .
@     brk .
@     fon,t .
@.
@     fon,t,'Arial',08,pdb .
@     btn,<win1>,28,52,02,08 'Finish' <exit>i6 .
@     fon,t .
@     inp,<exit> .
@0151:.
@     cls,<win1>  ldv <win1>=0 .
@     fon,r  fon,t .
@     win,,01,01,01,01,,h '' <win1>i6  cls,<win1>  ldv <win1>=0 .
@.
@0152:brk,0,A .
.Installation Notes
*===============================================================================
 <app> has been successfully installed in Cabinet <cab>.
 Please sign off as MAPCOORD and sign on as yourself.

@     lzr,-8 <tlns>i5,<tcc>i3,<thln>i3  if <thln> eq <tlns>  gto 0153 . ; .
 The app apears to have been installed successfully, but a few reports were
 skipped in the process.
@     rdc,-8 1-80 <tocrslt>s .
<tocrslt>


@. @     ldv <run>h12  rdl,-4,6,0152 'Run' <run> .
@. @.
@. @     if <dpt> ne mstrd$,(lin+2) ; .
@.  The following runs have been globally registered for all users:
@. @     if <dpt> eq mstrd$,(lin+2) ; .
@.  The following runs have been registered for all users in department <dpt>:
@.  Run Name
@.  --------
@. @     rdc,-4,6 'Run' <v1>h .
@.  <v1>
@. @0152:.
@0153:brk  rnm -8 .
@.
@     if <*deploy> eq 'Y'  gto 0199 . ; .
@     if <run> eq '',(lin+3) ; .
@     if <dpt> eq mstrd$ . ; if <dpt> eq depn$ . ; gto lin+2 .
fkey,01,''Quit  '',KEY
@     brk  fky,-0 .
@     dsp,-8 .
@     FKY .
@     if <run> eq '',(0199) ; .
@     if fkey$ eq 1,(0199) ; .
@.    BRK  DLR,-0 .
@.    SC q prep .
@.    IF <dpt> eq mstrd$  RUN <run> . ; IF <dpt> eq depn$  RUN <run> . ; .
@.    CLS,<win1>  LDV <win1>=0 .
@     gto 0199 .
@.
@.
@.    #  Subroutine:  Read SETUP.INF values
@0170:(<key>,<nestedkey>,<value>) .
@     ln+,-0,1,1  wrl,-0,2 2-1 *,= .
@     def,p <keysz>i3,<key> .
@     srh,-0,,,0173 dp 2-<keysz> [,<key>(p)] .
@     loc,-0,4,0173 afm 1-79 <nestedkey> ,<lin>i4  dec,1 <lin> .
@     inc <lin>  rdl,-0,<lin> 1-100 <dat>s .
@     lcv,0173 '' <dat> '=' <col>i2  inc <col> .
@     ldv <value>=<dat>(<col>-0) .
@0173:return  .
@.
@. --------------------------------- Errors ------------------------------------
@.
@0178:.
@     mbx,OK,S,'Could not locate SVCTBL.TXT' "/\
 Must BUILD application b4 implementing" \
      O,(0199) .
@0179:.
@     mbx,OK,S,'Could not locate CTGTBL.TXT' "/\
 Must BUILD application b4 implementing" \
      O,(0199) .
@0180:.
@     sc '' \
 CS;msg,1,e,,tic$'<Installation requires a Graphical Workstation>'tic$ .
@     rel .
@0181:.
@     mbx,OK,s,'Restriction' "/\
 Your Graphical Workstation or BIS level does not support SBS" \
      O,(0199) .
@0182:.
@     mbx,OK,S,'Insufficient Privileges' "/\
 You must have coordinator privileges to perform this installation." \
      O,(0199) .
@0183:.
@     mbx,OK,S,'Insufficient Privileges' "/\
 Your user-id is not permitted to generate a new drawer." \
      O,(0199) .
@0184:.
@     if <stp> eq 0,(0010) ; .
@     ldv <foc>=<edt1> .
@     mbx,OK,S,'Invalid Entry' "/\
 Path name contains invalid characters." \
      O,(0021) .
@0185:.
@     if <stp> eq 0,(0010) ; .
@     ldv <foc>=<edt1> .
@     mbx,OK,S,'Invalid Installation File(s)' "/\
 Installation file(s) not found in the path specified or are corrupt.//\
 Please verify the path name." \
      O,(0021) .
@0186:.
@     ldv <foc>=<edt1> .
@     mbx,OK,S,'Invalid Entry' "/\
 Invalid run registration department number." \
      O,(0041) .
@0187:.
@     mbx,OK,S,'Insufficient Database Space' "/\
 No empty drawers available this system." \
      O,(0199) .
@0188:.
@     ldv <foc>=<edt1> .
@     mbx,OK,S,'Invalid Entry' "/\
 You must specify an even-numbered cabinet." \
      O,(0053) .
@0189:.
@     ldv <foc>=<edt1> .
@     mbx,OK,S,'Invalid Entry' "/\
 Cabinet must be 16 through maxcab$." \
      O,(0053) .
@0190:.
@     ldv <foc>=<edt1> .
@     mbx,OK,S,'Invalid Entry' "/\
 Cabinet must be 2 through maxcab$." \
      O,(0053) .
@0191:.
@     ldv <foc>=<edt1> .
@     mbx,OK,S,'System Restriction' "/\
 Cabinets 200 through 250 are reserved and may not be used." \
      O,(0053) .
@0192:.
@     ldv <foc>=<edt2> .
@     mbx,OK,S,'Invalid Entry' "/\
 Invalid Drawer letter." \
      O,(0053) .
@0193:.
@     ldv <foc>=<edt1> .
@     mbx,OK,S,'Invalid Entry' "/\
 Database file number must be 3 through 'maxfil$" \
      O,(0061) .
@0194:.
@     if <stp> eq 0,(0010) ; .
@     ldv <foc>=<edt1> .
@     mbx,OK,S,'Invalid Installation File(s)' "/\
 Run drawer was not specified in SETUP.INF file.//\
 Please specify and try again." \
      O,(0021) .
@.
@. ------------------------------- Termination ---------------------------------
@.
@0196:. Abort
@.
@     if sysnam$(1-1) ne 'M'  chg <err>i4 cerr$ ; .
@     if sysnam$(1-1) eq 'M'  sc x EEL';'rsm ; .
@     rar 0196 .
@.
@0198:. Quit
@.
@     if <stp> ge 1 & le 7 . ; gto lin+4 .
@     mbx,YN,Q,'Confirmation' "/\
 Are you sure you want to abandon this installation?" \
      N,(lin+1),Y,(0199) .
@     if <stp> eq 1,(0011) ; .
@     if <stp> eq 2,(0021) ; .
@     if <stp> eq 3,(0031) ; .
@     if <stp> eq 4,(0041) ; .
@     if <stp> eq 5,(0053) ; .
@     if <stp> eq 6,(0061) ; .
@     if <stp> eq 7,(0075) ; .
@     if <stp> eq 8,(0151) ; .
@     gto 0199 .
@.
@0199:. Exit
@.
@     if <*deploy> eq 'Y'  return . ; cls,<win1> .
@     if drw8$ ne 0  lzr,-8,lin+1  dsx,-8 ; .
