.Object Name: Data_IF_Operands     Standardize @IF Statement Operands
.REV: 2.2    DATE: 20120218
*===============================================================================
*  Copyright (c) 2012 IT Resource Solutions (ITRS)                             *
*  --------------------------------------------------------------------------  *
*  The SBS utility is a free software package, provided for the benefit        *
*  of all BIS users.  It contains no warranty of any kind, and ITRS will       *
*  assume no liability for defects or problems resulting from its use.         *
*  This software may be used and distributed freely in its entire form;        *
*  however, no portion of this program code may be reused without the          *
*  expressed consent of ITRS.                                                  *
*===============================================================================
:L1=28,2=35,3=39,4=51,5=68,199=71
@.
@. ------------------------------- Test Setup ----------------------------------
@.
@     BRK,0,F .
@     RDC,EDRW$,50,2 1-80 <V1>s .
<V1>
@     BRK .
@.
@     CALL 0001 (<STS>h3) .
@     DSX,-0 .
@.
@. ------------------------------ Entry Point ----------------------------------
@.
@0001:(<STS>) .
@.
@     RNM -1 .
@     LZR,-1 ,<WID>i3  CHG <WWD>i3 <WID> - 1 .
@.
@     RNM,EDRW$,0006 -2 .
@     LDV <LN2>i4=5 .
@0002:INC <LN2>  RDL,-2,<LN2>,0199 'Find','Replace' <V1>h,<V2>h .
@     LDV,p <V1>,<FND>H9=@<V1>@  LCV b2 <FND> '@'/' ' .
@     LDV,p <V2>,<REP>H9=@<V2>@  LCV b2 <REP> '@'/' ' .
@     LDV <LIN>i4=2 .
@0003:INC <LIN>  LOC,-1,<LIN>,0002 aft? 1-<WID> <FND> <COL>i3,<LIN> .
@     RDL,-1,<LIN> 1-<WID> <DAT>s .
@     IF <DAT>(1-1) ne '@',(lin+4) ; LDV <N>i3=2 .
@     IF <DAT>(<N>-1) ne ' ',(lin+2) ; .
@     INC <N>  IF <N> gt <WID>,(0003) ; GTO lin-1 .
@     IF <DAT>(<N>-1) eq '.',(0003) ; GTO 0004 .
@     LDV <LB4>i4=<LIN> .
@     DEC <LB4>  IF <LB4> lt 3,(0003) ; RDL,-1,<LB4> 1-<WID> <V1>s .
@     LCV,0003 b1 <V1> '\'  IF <V1>(1-1) eq '@',(0004) ; GTO lin-1 .
@.
@. --------------------- Find & Translate @IF Operands -------------------------
@.
@0004:LDV <IF>h3='IF '  LCV,lin+1 b99t% <DAT>(1-<COL>) <IF> ,<N>i3 .
@     IF <N> gt 0,(lin+5) ; .
@     LDV <IF>h4='IF,c'  LCV,lin+1 b99 <DAT>(1-<COL>) <IF> ,<N>i3 .
@     IF <N> gt 0,(lin+3) ; .
@     LDV <IF>h4='IF,d'  LCV,lin+1 b99 <DAT>(1-<COL>) <IF> ,<N>i3 .
@     IF <N> eq 0,(0003) ; .
@     LCV,0003 b<N>t% <DAT>(1-<COL>) <IF> <X>i3  CHG <N>i3 <COL> + 1 .
@     DEC <N>  IF <N> le <X>,(lin+2) ; IF <DAT>(<N>-2) ne '. ',(lin-0) ; .
@     GTO 0003 .
@     LCV b1t% <DAT>(<COL>-0) <FND>/<REP> .
@     RDL,-1,<LIN> 1-<WID> <OLN>s  DEF,p <OSZ>i3 <OLN>  DEF,p <NSZ>i3 <DAT> .
@     IF <NSZ> le <OSZ>,(0005) ; CHG <SZ>i1 <NSZ> - <OSZ> .
@     LDV <EOC>i3=1 .
@     LCV,0005 b1t~ <DAT>(<EOC>-0) ' . ' <EOC>i3  INC,3 <EOC> .
@     LCV,lin+2 b1t~ <DAT>(<EOC>-0) ';' <SEM>i3 .
@     CHG <EOC> <SEM> + 1  GTO lin-2 .
@     IF <EOC> lt <WID>  LCV,0005 b<SZ>t~ <DAT>(<EOC>-0) '  '/' ' ; .
@0005:IF,c <DAT> eq <OLN>,(0003) ; .
@     WRL,-1,<LIN> 2-<WWD> <DAT>(1-1),<DAT>(2-0)  DEC <LIN>  GTO 0003 .
@.
@0199:. --------------------------- Return -------------------------------------
@.
@     RNM,-1 -0 .
@     LDV <STS>=OK .
@     RETURN .
