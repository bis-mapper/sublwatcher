.Object Name: SUBLCOMMON
.
.
.
*
*
*===============================================================================
:INCLUDE,ecab$,B,1 .
@.
@.    #  Break apart <run> into useable <cab>,<drw>,<rpt>
@0001:(<run>,<cab>,<drw>,<rpt>) .
@     lcv,lin+1 b1 <run> 'a' <alpha>i3  gto 0002 .
@     lcv,lin+1 b1 <run> 'b' <alpha>i3  gto 0002 .
@     lcv,lin+1 b1 <run> 'c' <alpha>i3  gto 0002 .
@     lcv,lin+1 b1 <run> 'd' <alpha>i3  gto 0002 .
@     lcv,lin+1 b1 <run> 'e' <alpha>i3  gto 0002 .
@     lcv,lin+1 b1 <run> 'f' <alpha>i3  gto 0002 .
@     lcv,lin+1 b1 <run> 'g' <alpha>i3  gto 0002 .
@     lcv,lin+1 b1 <run> 'h' <alpha>i3  gto 0002 .
@     lcv,lin+1 b1 <run> 'i' <alpha>i3 .
@0002:ldv <drw>=<run>(<alpha>-1)  dec <alpha> .
@     ldv <rpt>=<run>(1-<alpha>)  inc,2 <alpha> .
@     ldv <cab>=<run>(<alpha>-0) .
@     return .
@.
@.    #  Load the paramaters from the config table
@0005:srh,SUBLCONFIG_ 'dn@' 'Parameter' ¬,@ .
@     lzr,-0 ,,<hln>i7  .
@     dvs,-0 'Value' <t>s  def,s <len>i3,<t> .
@0006:inc <hln> .
@     rdl,-0,<hln>,0008 'Parameter','Value','Ty','Siz',\
        'Opt' <param>s,<value>s,<ty>h,<siz>i,<opt>h .
@     ldv <'>h1=tic$ .
@     lcv,lin+1 '' <value> '$'  ldv <'>h0 .                                     Is Reserve Word?
@     lcv,lin+1 '' <value> '<'  ldv <'>h0 .                                     Is Variable
@     xqt '@ ldv'<opt>(p)' <'<param>(p)'>'<ty><siz>(p)'='<'><VALUE><'> .
@     gto 0006 .
@0008:esr .
@.
@.    #  Check for ICE Service return Service Title if ICE Enabled site
@0010:(<cab>,<drw>,<rpt>,<srvs>) .
@     ldv <svc>s21 .
@     if <rpt> eq 0,(0012) .
@.ew  rer 0012 .
@     ldv,l <cab>  ldv,r <rpt>  ldv <svc>s20 .
@     ldv,p <report>h11='1d3950' .
@     DIR <report> ,,,,,<acc>h1  if <acc> eq '',(0013) .
@     lzr,3950,d,1,0011 .
@     fdr,3950,d,1,,,0011 '' 'SvcCab','sd' ¬,<cab>,<drw> .
@     rln 'cab','dw','rpt' <icab>h,<idw>h,<irpt>h .
@     fdr,<icab>,<idw>,<irpt>,,,0011 '' 'cab','dw','rpt' ¬,<cab>,<drw>,<rpt> .
@     rln 'service' <svc>s  gto 0012 .
@0011:fdr,<cab>,<drw>,<rpt>,2,,0012 'a' 2-12 .,'Object Name:' ,<vlno>i3 .
@     rln 14-21 <svc>s  gto 0012 .
@0012:if <svc> eq ' '  ldv,p <srvs>=<svc> . ; ldv,p <srvs>='('<svc>(p)')' .
@     return .
@.
@.    #  Check if folder exists.  If not create it.
@0015:(<dir>,<fldr>,<fldrpath>) .
@     pcr,0,A,-0,n,n,lin+2 <dir> .
@     loc,-0,2,lin+1 afm 2-63 '['<fldr>']'  gto lin+2 .
@     pc,bcw 'MD '"<fldrpath>" .
@     return .
@.
@.    #  Get computer name
@0020:(<computer>) .
@     fdr,SUBLCONFIG_ '' 'Parameter' ¬,'BTEMP' .
@     rln 'Parameter','Value','Ty','Siz','Opt' \
        <param>s,<value>s,<ty>h,<siz>i,<opt>h .
@     ldv <'>h1=tic$ .
@     xqt '@ ldv'<opt>(p)' <'<param>(p)'>'<ty><siz>(p)'='<'><VALUE><'> .
@.
@     pcr,0,A,-0,,,lin+1,t <btemp>'\hostname.txt'  gto 0022 .                   File exist
@     pc,bcw 'HOSTNAME >'<btemp>'\hostname.txt' .                               Create and pipe output
@     pcr,0,A,-0,,,0198,t <btemp>'\hostname.txt' .                              Read file
@0022:rdl,-0,2 1-30 <computer>s .                                               Get name
@     return .
@.
@.    #  Check if program exists already in folder.
@.    #  If it doesn't exist load <FILE> to default site location
@.    #  If it does exist load <file> to current location
@.    #  If multiple are found it makes reference to the last one as the one in
@.    #  sub folder has higher precedence over one in the default folder.
@.
@.    @. #  Find program in folder(s) and return filepath
@.    @  CALL,SUBLCOMMON_ searchFolders_ (<folder>,<program>,<file>s256) .
@0025:(<fldrpath>,<program>,<file>) .
@     fdr,SUBLCONFIG_ '' 'Parameter' ¬,'BTEMP' .
@     rln 'Parameter','Value','Ty','Siz','Opt' \
        <param>s,<value>s,<ty>h,<siz>i,<opt>h .
@     ldv <'>h1=tic$ .
@     xqt '@ ldv'<opt>(p)' <'<param>(p)'>'<ty><siz>(p)'='<'><VALUE><'> .
@     pc,cbw "WHERE /r <fldrpath>(p) <program>(p) ><btemp>\where.txt" .
@     pcr,0,F,-0,,,0198,t <btemp>'\where.txt' .                              Read file
@     lzr,-0 <lns>i7,,<hln>i3 .
@     if <lns> gt <hln> . ; gto 0029 .
@     rdl,-0,<lns> 1-256 <file> .
@0029:return .
@.
@.    #  Get file name from output file
@.    #  getFileName_
@0035:(<file>,<run>) .
@     dvs,SUBLOG_ 'File' <tfile>s  ldv <tfile>=<file> .
@     lcv 'b99' <tfile> '\' ,<occ>i3 .                                          Find last occurance
@     lcv 'b<occ>' <tfile> '\' <pos>i3  inc <pos> .                             Last \ pos
@     ldv,p <run>=<tfile>(<pos>-0) .                                            Load BIS run
@     lcv,0039 'b1' <run> '(' <lpos>i3 .                                        Find pos
@     lcv 'b1' <run> ')' <rpos>i3 .                                             Find pos
@     ldv <run>(<lpos>-<rpos>)='' .                                             Remove name
@0039:.
@     return .
@.
@.    #  Get APPPATH if it exists
@.    #  appPath_
@0040:(<user>,<apppath>) .
@     pcr,0,i,-0,,,lin+1,t '%USERPROFILE%\sublwatcher\SUBLCONFIG.INF'  gto lin+3 .       File exist?
@     call,SUBLSETUP_ 0001 (<user>,<apppath>s256) .                                   No? goto Setup
@     gto lin-2 .
@     call,SUBLCOMMON_ readConfigInf_ ('CONFIG','apppath',<apppath>s80) .
@0041:return .
@.
@.    #  Get Windows Version
@0045:(<ver>) .
@     fdr,SUBLCONFIG_ '' 'Parameter' ¬,'BTEMP' .
@     rln 'Parameter','Value','Ty','Siz','Opt' \
        <param>s,<value>s,<ty>h,<siz>i,<opt>h .
@     ldv <'>h1=tic$ .
@     xqt '@ ldv'<opt>(p)' <'<param>(p)'>'<ty><siz>(p)'='<'><VALUE><'> .
@     pcr,0,A,-0,,,lin+1,t <btemp>'\ver.txt'  gto 0047 .                        File exist
@     pc,bcw 'VER >'<btemp>'\ver.txt' .                                         Create and pipe output
@     pcr,0,A,-0,,,0198,t <btemp>'\ver.txt' .                                   Read file
@0047:rdl,-0,3 1-80 <over>s .                                                   Read output
@     lcv,lin+2  'b1' <over> '[Version' <vpos>i3 .                              Locate version
@     inc,9 <vpos>  ldv <ver>=<over>(<vpos>-1) .                                Get ver
@     return .
@.
@.    #  Get CHGSFILE path
@0050:(<appdir>,<chgsfile>) .
@     call 0045 (<osver>i1) .
@     ldv <'>h1=tic$ .
@     if <osver> eq 5 . ; gto 0051 .                                            Windows XP
@     fdr,SUBLCONFIG_ '' 'Parameter' ¬,'BTEMP' .
@     rln 'Parameter','Value','Ty','Siz','Opt' \
        <param>s,<value>s,<ty>h,<siz>i,<opt>h .
@     xqt '@ ldv'<opt>(p)' <'<param>(p)'>'<ty><siz>(p)'='<'><VALUE><'> .
@     ldv,p <chgsfile>=<btemp>\site-netsit$\changes.txt .
@     gto 0054 .
@0051:fdr,SUBLCONFIG_ '' 'Parameter' ¬,'CHGSFILE' .
@     rln 'Parameter','Value','Ty','Siz','Opt' \
        <param>s,<value>s,<ty>h,<siz>i,<opt>h .
@     lcv,lin+1 '' <value> '$'  ldv <'>h0 .                                     Is Reserve Word?
@     lcv,lin+1 '' <value> '<'  ldv <'>h0 .                                     Is Variable
@     xqt '@ ldv'<opt>(p)' <'<param>(p)'>'<ty><siz>(p)'='<'><VALUE><'> .
@0054:return .
@.
@.    #  Replace tab character with special character
@.    #  Pass 'export' to convert tab to special
@.    #  Pass 'import' to convert special to tab
@0055:(<direction>) .
@     rnm -1 .
@     chg <ns>h1 chr$254  lzr,-1 ,<cc>i4 .
@     chg <ns1>h1 chr$302 .
@     ldv <char>h0 .
@.
@     if <direction> eq export . ; gto 0056 .
@     lch,-1,,lin+1 'af' 1-<cc> <ns1>tab$/<ns>  rnm -1 .
@     lch,-1,,lin+1 'af' 1-<cc> tab$/<ns>  rnm -1 .
@     lch,-1,,lin+1 'af' 1-<cc> <ns1><ns>/<ns>  rnm -1 .
@. @     LCH,-1,,lin+1 'af' 1-<cc> <ns1>/''   RNM -1 .
@     gto 0059 .
@.
@0056:if <direction> eq import . ; gto 0059 .
@     lch,-1,,lin+1 'af' 1-<cc> <ns1><ns>/tab$  rnm -1 .
@     lch,-1,,lin+1 'af' 1-<cc> <ns1>/''   rnm -1 .
@     lch,-1,,lin+1 'af' 1-<cc> <ns>/tab$  rnm -1 .
@.
@0059:return .
@.
@.    #---------------------------------------------------------------------------------------------
@.    #  Read setup.inf data.
@.    #
@.    #  readConfigInf_
@.    #
@.    #  @     call,SUBLCOMMON_ readConfigInf_ (<key>,<nestedkey>,<value>s20) .
@.    #
@.    #  <key>        'revision'
@.    #  <nestedkey>  'level'
@.    #  <value>      '0.0.1'
@.    #
@.    #  [revision]
@.    #  level=0.0.1
@.    #  [name]
@.    #  name=sample
@.    #  [title]
@.    #  title=
@.    #  [drawers]
@.    #  drawer=b,998,tables
@.    #  drawer=c,132,lib
@.    #  drawer=d,998,lib_tables
@.    #  drawer=e,256,schema
@.    #  drawer=f,132,sample
@.    #  drawer=g,256,params
@.    #  drawer=h,132,views
@.    #  drawer=i,132,models
@.    #
@.    #---------------------------------------------------------------------------------------------
@0060:(<key>,<nestedkey>,<value>) .
@     ln+,-0,1,1  wrl,-0,2 2-1 *,= .
@     def,p <keysz>i3,<key> .
@     srh,-0,,,0061 dp 2-<keysz> [,<key>(p)] .
@     loc,-0,4,0061 afm 1-79 <nestedkey> ,<lin>i4  dec,1 <lin> .
@     inc <lin>  rdl,-0,<lin> 1-100 <dat>s .
@     lcv,0061 '' <dat> '=' <col>i2  inc <col> .
@     ldv <value>=<dat>(<col>-0) .
@0061:return .
@.
@.
@.
@.
@.
@.    #  Errrrrrorrrr
@0198:.
@.
@     return .
