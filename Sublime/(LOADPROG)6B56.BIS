.Object Name: LOADPROG
*===============================================================================
*  RADS - Rapid Application Development Studio                                 *
*  Copyright (c) 2013 IT Resource Solutions (ITRS)                             *
*  Execution of this program without a valid software license, or reuse        *
*  of this program code without expressed consent of ITRS, is prohibited.      *
*==============================================================================*
:INCLUDE,ECAB$,B,1 .
@.
@. ------------------------------- Test Setup ----------------------------------
@.
@.
@     LDV,p <NAM>h8='MFXFILE' .
@     CALL 0001(<NAM>,<PTH>s64,<PRG>s30,<STS>h3) .
<PTH>
<PRG>
<STS>
@     GTO END .
@.
@. ---------------------------- Call Entry Point -------------------------------
@.
@0001:(<NAM>,<PTH>,<PRG>,<STS>) .
@.
@. -------------------------- Module Initialization ----------------------------
@.
@     LDV <WIN1>i6=0 .
@.
@. ---------------------- Get Program Location & Version -----------------------
@.
@     FDR,SUBLDEP_,,,0199 '' 'Name' ¬,<NAM> .
@     RLN 'Rpt','File','Ext','Rep','Rev','Description' \
      <RPT>i,<FIL>s,<EXT>h,<REP>h,<REV>a,<DES>s .
@     LDV,p <NAM>,<FIL>,<EXT>,<PRG>=<FIL>'.'<EXT>,<DES> .
@.
@     CALL,EDRW$,0005 0001(<PTX>s64) .
@.
@. -------------------------- Check Version Control ----------------------------
@.
@     PCR,0,A,-0,n,n,0199 <PTX>  RNM -1 .
@     LOC,-1,2,0002 afm 2-11 <PRG> .
@     LOC,-1,2,0002 afm 2-11 <NAM>'.VER' .
@     PCR,0,A,-0,,,0002 <PTX><NAM>'.VER' .
@     RDL,-0,2,0002 1-3 <REX>a .
@     IF <REV> eq <REX>,(0003) ; .
@0002:.
@.
@. @     LDV,p <MSG>s64='Loading Object: '<DES> .
@. @     CALL,EDRW$,0009 0001('0',<WIN1>,'',<MSG>) .
@.
@     LZR,ECAB$,E,<RPT>,0180 .
@     PCW,ECAB$,E,<RPT>,3,0180 <PTX><PRG> .
@     BRK,0,A .
<REV>
@     BRK  PCW,-0,2,0180 <PTX><NAM>'.VER' .
@     CLS,<WIN1>  LDV <WIN1>=0 .
@0003:.
@     DEF,s <N>i3,<PTH>  IF <N> ne 0  LDV,p <PTH>=<PTX> ; .
@     LDV <STS>h3=OK .
@     GTO 0199 .
@.
@. ------------------------------- Errors --------------------------------------
@.
@0180:.
@     CLS,<WIN1>  LDV <WIN1>=0 .
@     CALL,EDRW$,0010 0001*('OBJECTLOAD',<PRG>,'') .
@     GTO 0199 .
@.
@0199:.--------------------------- Termination ---------------------------------
@.
@     CLS,<WIN1> .
@     RETURN .
