.SUBL_Setup
.
.
.
*
*
*===============================================================================
:INCLUDE,ECAB$,B,1 .
@.
@.    #  Test Setup
@ rdb LDV <USER>h11=USER$ .
@     CALL 0001(<USER>,<APPPATH>s256) .
@     GTO END .
@.
@.    #  Call Entry Point
@0001:(<USER>,<APPPATH>) .
@.
@.    #  Load config vars
@     RSR,SUBLCOMMON_ configVars_ .
@.
@.    #  Get computer name
@     CALL,SUBLCOMMON_ getCompName_ (<COMPUTER>s30) .
@.    #  Get OS level (XP=5,W7=6,...)
@     CALL,SUBLCOMMON_ getOsVer_ (<OSVER>i1) .
@     CALL,SUBLCOMMON_ getChgsFile_ (<APPDIR>,<CHGSFILE>s256) .
@.
@.    #  Prompt user to find Editor
@     MBX,OC,I,'SUBL' \
"It looks like this is the first time running SUBL. //\
 In order to continue you need to set up your application path. //\
 Click ok and browse to sublime_text.exe (usually in program files)//"\
 O,(lin+1),C,(0199) .
@.
@. @.    #  Create temp folder
@. @     LDV,p <DIR>s256=<BTEMP>'\' .
@. @     LDV,p <FLDR>s256=<APP> .
@. @     LDV,p <FLDRPATH>s256=<TMPPTH> .
@. @     CALL,SUBLCOMMON_ chkMkDir_ (<DIR>,<FLDR>,<FLDRPATH>) .
@.
@.    #  Create [sublwatcher] folder
@     LDV,p <DIR>s256=<APPDATA>'\' .
@     LDV,p <FLDR>s256=<APP> .
@     LDV,p <FLDRPATH>s256=<APPDIR> .
@     CALL,SUBLCOMMON_ chkMkDir_ (<DIR>,<FLDR>,<FLDRPATH>) .
@.
@.    #  Create folder for current site if it doesnt exist
@     LDV,p <DIR>s256=<APPDIR>'\' .
@     LDV,p <FLDR>s256=<SITE> .
@     LDV,p <FLDRPATH>s256=<DATADIR> .
@     CALL,SUBLCOMMON_ chkMkDir_ (<DIR>,<FLDR>,<FLDRPATH>) .
@.
@.    #  Create batch file
@     IF <OSVER> eq 5  \
        SRH,BATCHTEMP_,,,0000 'dp' 'OS' ¬,'WINDOWS XP' . ; \
        SRH,BATCHTEMP_,,,0000 'dp' 'OS' ¬,'WINDOWS' .
@     LZR,-0 ,<CC>I3,<SLN>I7  LOK,-0  LN-,-0,2,<SLN>  ULK .
@     RNM -1  LZR,-1 ,<CC>I3,<SLN>I7  BRK,0,I .
@     RDL,-1,<SLN>,lin+3 1-<CC> <BATPRGM>s  INC <SLN> .
@     XQT <BATPRGM> .
@     GTO lin-2 .
@     BRK,0,F .
@     PCW,-0 <BATFILE> .
@.
@.    #  Make sure exploere utility is loaded onto machine
@     LZR,SUBLDEP_ ,,<HLN>i3 .
@     INC <HLN>  RDL,SUBLDEP_,<HLN>,lin3 'NAME' <DEPNAME>s .
@     CALL,EDRW$,0006 0001(<DEPNAME>,<PTX>s64,<PRG>s30,<STS>h3) .
@     GTO LIN-2 .
@.
@.    #  Open windows explorer using MFXFILE.EXE
@     LDV <ACT>h4=OPEN .
@     CALL,EDRW$,0004 0001 (<ACT>,<PTH>s256,<FIL>s64,<SPT>s64,<SFL>h12,<TYP>h1,<STS>h3) .
@.
@     IF <STS> ne 'OK ',(0199) .
@.
@.    #  Save programe .exe path to SUBLPATH table
@     LDV,p <APPPATH>=<PTH>(p)'\'<FIL>(p) .
@     FDR,SUBLPATH_ '' 'User ID','Computer Name' ¬,<USER>,<COMPUTER> ,<LNS>i3 .
@     LOK,SUBLPATH_ .
@     IF <LNS> gt 0 . ; LN+,SUBLPATH_,,1  LDV <LNS>i7=STAT1$ .
@     WRL,SUBLPATH_,<LNS>,y 'User ID','Computer Name','App Path' ¬,\
        <USER>,<COMPUTER>,<APPPATH> .
@     ULK .
@.
@.    #  Load watchmedo.exe
@     CALL,EDRW$,0006 0001('WATCHMEDO',<PTX>s64,<PRG>s30,<STS>h3) .
@.
@.    #  Load watchmedo-script.py
@     CALL,EDRW$,0006 0001('WATCHMEDO-SCRIPT',<PTX>s64,<PRG>s30,<STS>h3) .
@.
@0199:. -------------------------- Termination ---------------------------------
@.
@     RETURN .
