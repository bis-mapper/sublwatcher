.SUBL
.
.
.
*
*
*===============================================================================
:INCLUDE,ECAB$,B,1 .                                                            INCLUDE
@.
@     LDV <CALL>h1=n .                                                          Manual entrance
@.
@.    #  Call entry point
@0001:(<RUN>) .                                                                 Call from BisCmd?
@     RER,0,E,2 0001  RAR,0,E,2 0001 .                                          sets-run-error
@.
@     IF <RUN> eq '<RUN>',(0005) .                                              Normal or Call?
@     LDV <CALL>h1=y .
@     IF <RUN> eq 'SETUP',(0007) .
@     CALL,SUBLCOMMON_ getCabDrwRpt_ (<RUN>,<CAB>i4,<DRW>h1,<RPT>i4) .
@     GTO 0007 .
@.
@.    #  Otherwise we get report on display
@.    #  This happens when user enters SUBL on a given report
@0005:LDV <CAB>i4=CAB$,<DRW>h1=ADRW$,<RPT>i4=RPT$ .                             Get RPT on display
@     CHG INPUT$ <CMD>s100 .
@.
@.    #  Load the application configuration variables from table
@0007:RSR,SUBLCOMMON_ configVars_ .
@     IF <RPT> eq 0  REL . ; .                                                  No report on display?
@     CALL,SUBLCOMMON_ chkIceSvc_ (<CAB>,<DRW>,<RPT>,<SRVS>s30) .               Ice Service?
@     LDV,p <SRVS> .
@.
@.    #  First time running program...Enable setup
@     CALL,SUBLCOMMON_ appPath_ (<USER>,<APPPATH>s256) .
@     IF <APPPATH> eq ' ',(0199) .                                              Exit
@.
@.    #  Create folder for current site if it doesnt exist
@0010:LDV,p <DIR>s256=<APPDIR>'\' .
@     LDV,p <FLDR>s256=<SITE> .
@     LDV,p <FLDRPATH>s256=<DATADIR> .
@     CALL,SUBLCOMMON_ chkMkDir_ (<DIR>,<FLDR>,<FLDRPATH>) .
@.
@.    #  Check if program exists already in folder.
@.    #  If it doesn't exist load <FILE> to default site location
@.    #  If it does exist load <FILE> to current location
@     LDV <FOLDER>s256=<DATADIR>(p)'\' .
@     LDV <PROGRAM>s40=<SRVS><RPT>(p)<DRW>(p)<CAB>(p)<EXT> .
@     CALL,SUBLCOMMON_ searchFolders_ (<FOLDER>,<PROGRAM>,<FILE>s256) .
@.
@     RSL,<CAB>,<DRW>,<RPT> .                                                   Get result
@     RNM -1 .
@     PCW,-1,2 <FILE> .                                                         Write file
@.
@.    #  Start text editor
@.    #  Refer to "http://www.sublimetext.com/docs/2/osx_command_line.html" for all options
@     PC,r '"'<APPPATH>(p)'" -a' '"'<FILE>':'<DLN>(p)'"' .                      start editor with options
@     SYS .                                                                     system
@     IF <CALL> ne Y  PC 'cmd /C '"<BATFILE>" . ; .                             start / check if running
@     FDR,-0,,,lin+1 '' 'USERID','RUNNAME'   	,USER$,<SUBLWATCHER>  GTO 0199 .   currently running?
@     WAT 1000 .                                                                pause for first time
@     RUN <SUBLWATCHER> .                                                       start file watcher utility
@.
@.    #  Return if called otherwise display current report
@0199:IF <CALL> eq y  RETURN . ; DSP,<CAB>,<DRW>,<RPT> .
@.
