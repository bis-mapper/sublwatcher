.SUBL
.
.
.
*
*
*===============================================================================
:INCLUDE,ECAB$,B,1 .                                                            INCLUDE
@.
@     CHG INPUT$ <RUNINPUT>s20 .
@     LDV <CALL>h1=n,<DLN>i7=DLINE$ .                                           Manual entrance
@.
@.    #  Call entry point
@0001:(<RUN>) .                                                                 Call from BisCmd?
@     RER 0198 .
@     RAR,0,E,2 0001 .                                                          sets-run-error
@.
@     IF <RUN> eq '<RUN>',(0005) .                                              Normal or Call?
@     LDV <CALL>h1=y,<DLN>s0 .
@     CALL,SUBLCOMMON_ getCabDrwRpt_ (<RUN>,<CAB>i4,<DRW>h1,<RPT>i4) .
@     LDV,u <CAB>,<DRW>,<RPT> .
@     GTO 0007 .
@.
@.    #  Otherwise we get report on display
@.    #  This happens when user enters SUBL on a given report
@0005:LDV <CAB>i4=CAB$,<DRW>h1=ADRW$,<RPT>i4=RPT$ .                             Get RPT on display
@     CHG INPUT$ <CMD>s100 .
@.
@.    #  Load the application configuration variables from table
@0007:RSR,SUBLCOMMON_ configVars_ .
@     IF <RPT> eq 0 REL . ; .                                                  No report on display?
@     CALL,SUBLCOMMON_ chkIceSvc_ (<CAB>,<DRW>,<RPT>,<SRVS>s30) .               Ice Service?
@     LDV,p <SRVS> .
@.
@.    #  First time running program...Enable setup
@     CALL,SUBLCOMMON_ appPath_ (<USER>,<APPPATH>s256) .
@     IF <APPPATH> eq ' ',(0199) .                                              Exit
@.
@.    #  Create folder for current site if it doesnt exist
@0010:LDV,p <DIR>s256=<APPDIR>'\' .
@     LDV,p <FLDR>s256=<SITE> .
@     LDV,p <FLDRPATH>s256=<datadir> .
@     CALL,SUBLCOMMON_ chkMkDir_ (<DIR>,<FLDR>,<FLDRPATH>) .
@.
@.    #  Read quick open flag
@     CALL,SUBLCOMMON_ getCompName_ (<COMPUTER>s30) .
@     FDR,SUBLPATH_,,,lin+2 ' ' 'User ID','Computer Name' ¬,<USER>,<COMPUTER> . Get Application Path
@     RLN 'Quick Open' <QUICKOPEN>s .
@     IF <RUNINPUT> ne '<RUNINPUT>'  IF <RUNINPUT>(p) eq 'q'  LDV <QUICKOPEN>s7='ENABLED' . ; .
@.
@.    #  Check if program exists already in folder.
@.    #  If it doesn't exist load <FILE> to default site location
@.    #  If it does exist load <FILE> to current location
@     LDV <FOLDER>s256=<DATADIR>(p)'\' .
@     LDV <PROGRAM>s40=<SRVS><RPT>(p)<DRW>(p)<CAB>(p)<EXT> .
@     CALL,SUBLCOMMON_ getOsVer_ (<OSVER>i1) .
@     IF <OSVER> gt 5 . ; gto 0014 .
@     CALL,SUBLCOMMON_ searchFolders_ (<FOLDER>,<PROGRAM>,<FILE>s256) .
@     IF <FILE> eq ' ' . ; GTO 0015 .
@     IF <QUICKOPEN> eq 'ENABLED',(0014) .
@     LDV <ACT>h4=DIR,<PTH>s256 .
@     CALL,EDRW$,0004 0001 (<ACT>,<PTH>s256,<FIL>s256,'','',<TYP>h1,<STS>h3) .
@     IF <STS> eq OK . ; GTO 0199 .
@     LDV <FILE>s256=<PTH>(p)'\'<PROGRAM> .
@     GTO 0015 .
@0014:LDV <FILE>s256=<FOLDER>(P)<PROGRAM> .
@0015:LDV,p <FILE> .
@.
@     RSL,<CAB>,<DRW>,<RPT> .                                                   Get result
@     CALL,SUBLCOMMON_ charReplace_ ('export') .                                Replace tab character
@     RNM -1 .
@.
@     PCW,-1,2,lin1 <FILE>  gto 0020 .                                          Write file
@     ldv <MSG>s80='Could not write out '<file> .
@     CALL,GROWL_ 0001 ('SUBL Failed','FAILED',<MSG>) .
@     gto 0199 .
@.
@.    #  Start text editor
@.    #  Refer to "http://www.sublimetext.com/docs/2/osx_command_line.html" for all options
@0020:PC,r '"'<APPPATH>(p)'" -a' '"'<FILE>':'<DLN>(p)'"' .                      start editor with options
@     SYS .                                                                     system
@0025:FDR,-0,,,lin+1 '' 'USERID','RUNNAME' ¬,USER$,<SUBLWATCHER>  GTO 0199 .    currently running?
@     WAT 1000 .                                                                pause for first time
@     RUN <SUBLWATCHER> .                                                       start file watcher utility
@.
@.    #  Output Growl error message
@0198:CHG <ERR>i4 CERR$  LSM,<ERR> <MSG>s88 .
@     IF <MSG> eq ' '  LDV <MSG>='That request cannot be processed' .
@     CALL,GROWL_ 0001 ('BISCMD Failed','FAILED',<MSG>) .
@.
@.    #  Return if called otherwise display current report
@0199:IF <CALL> eq y  RETURN . ; DSP,<CAB>,<DRW>,<RPT> .
@.
