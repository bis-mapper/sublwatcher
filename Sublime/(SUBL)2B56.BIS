.Object Name: SUBL
.
.
.
*
*
*===============================================================================
:INCLUDE,ecab$,B,1 .                                                            INCLUDE
@.
@     chg input$ <runinput>s20 .
@     ldv <call>h1=n,<dln>i7=dline$ .                                           Manual entrance
@.
@.    #  Call entry point
@0001:(<run>) .                                                                 Call from BisCmd?
@     SUBLRER_ .
@.
@     if <run> eq '<RUN>',(0005) .                                              Normal or Call?
@     ldv <call>h1=y,<dln>s0 .
@     lcv,lin+1 '' <run> '-q'/ <qpos>i3  ldv <quick>h1='Y' .
@     call,SUBLCOMMON_ getCabDrwRpt_ (<run>,<cab>i4,<drw>h1,<rpt>i4) .
@     ldv,u <cab>,<drw>,<rpt> .
@     gto 0007 .
@.                                                                                                                   Some odhdfhdfhd
@.    #  Otherwise we get report on display
@.    #  This happens when user enters SUBL on a given report
@0005:ldv <cab>i4=cab$,<drw>h1=adrw$,<rpt>i4=rpt$ .                             Get RPT on display
@     chg input$ <cmd>s100 .
@     if <rpt>(p) eq '-0' gto lin+2 . ; .
@     if <cab> eq 0,''  if <rpt> eq 0,'' . ; gto 0007 .
@     brk,0,a .
   #--------------------------------------------------------------------------------#
   #                                                                                #
   #             The SUBL command can only be used on an actual report.             #
   #                                                                                #
   #          To open a result please create a report of the result first.          #
   #                                                                                #
   #--------------------------------------------------------------------------------#
@     if <call> eq y  return . ; gto end .
@.
@.    #  Load the application configuration variables from table
@0007:rsr,SUBLCOMMON_ configVars_ .
@.ew  if <rpt> eq 0  rel . ; .                                                  No report on display?
@     call,SUBLCOMMON_ chkIceSvc_ (<cab>,<drw>,<rpt>,<srvs>s30) .               Ice Service?
@     ldv,p <srvs> .
@.
@.    #  First time running program...Enable setup
@     call,SUBLCOMMON_ appPath_ (<user>,<apppath>s256) .
@     if <apppath> eq ' ',(0199) .                                              Exit
@.
@.    #  Create folder for current site if it doesnt exist
@0010:ldv,p <dir>s256=<appdir>'\' .
@     ldv,p <fldr>s256=<site> .
@     ldv,p <fldrpath>s256=<datadir> .
@     call,SUBLCOMMON_ chkMkDir_ (<dir>,<fldr>,<fldrpath>) .
@.
@.    #  Read quick open flag
@     pcr,0,i,-0,,,lin+1,t '%USERPROFILE%\sublwatcher\SUBLCONFIG.INF'  gto lin+3 .       File exist?
@     call,SUBLSETUP_ 0001 (<user>,<apppath>s256) .                                   No? goto Setup
@     gto lin-2 .
@     call,SUBLCOMMON_ readConfigInf_ ('CONFIG','quickopen',<quickopen>s8) .
@     if <runinput> ne '<RUNINPUT>'  if <runinput>(p) eq 'q'  ldv <quickopen>s7='ENABLED' . ; .
@.
@.    Check if report exists, if not notify user and bail out.
@     lzr,<cab>,<drw>,<rpt>,lin+1  gto 0011 .
@     ldv <msg>s80='Report '<rpt>(p)<drw><cab>(p)' was not found!' .
@     call,GROWL_ 0001 ('Sublwatcher: Failed!','FAILED',<msg>) .
@     gto 0199 .
@0011:.
@.
@.    #  Check if program exists already in folder.
@.    #  If it doesn't exist load <FILE> to default site location
@.    #  If it does exist load <file> to current location
@0012:ldv <folder>s256=<datadir>(p)'\' .
@     ldv <program>s40=<srvs><rpt>(p)<drw>(p)<cab>(p)<ext> .
@     call,SUBLCOMMON_ getOsVer_ (<osver>i1) .
@     if <osver> gt 5 . ; gto 0014 .
@     call,SUBLCOMMON_ searchFolders_ (<folder>,<program>,<file>s256) .
@     if <file> eq ' ' . ; gto 0015 .
@     if <quickopen> eq 'ENABLED',(0014) . ; if <quick> eq 'Y',(0014) . ; .
@     ldv <act>h4=DIR,<pth>s256 .
@     call,edrw$,0004 0001 (<act>,<pth>s256,<fil>s256,'','',<typ>h1,<sts>h3) .
@     if <sts> eq OK . ; gto 0199 .
@     ldv <file>s256=<pth>(p)'\'<program> .
@     gto 0015 .
@0014:ldv <file>s256=<folder>(P)<program> .
@0015:ldv,p <file> .
@.
@     rsl,<cab>,<drw>,<rpt> .                                                   Get result
@     call,SUBLCOMMON_ charReplace_ ('export') .                                Replace tab character
@     rnm -1 .
@.
@     pcw,-1,2,lin+1 <file>  gto 0020 .                                         Write file
@     ldv <msg>s80='Could not write out '<file> .
@     call,GROWL_ 0001 ('SUBL Failed','FAILED',<msg>) .
@     gto 0199 .
@.
@.    #  Start text editor
@.    #  Refer to "http://www.sublimetext.com/docs/2/osx_command_line.html" for all options
@0020:pc,r '"'<apppath>(p)'" -a' '"'<file>':'<dln>(p)'"' .                      start editor with options
@     sys .                                                                     system
@0025:fdr,-0,,,lin+1 '' 'USERID','RUNNAME' ¬,user$,<sublwatcher>  gto 0199 .             currently running?
@     wat 1000 .                                                                pause for first time
@     RUN <sublwatcher> .                                                       start file watcher utility
@.
@.    #  Output Growl error message
@0198:chg <err>i4 cerr$  lsm,<err> <msg>s88 .
@     if <msg> eq ' '  ldv <msg>='That request cannot be processed' .
@     call,GROWL_ 0001 ('BISCMD Failed','FAILED',<msg>) .
@.
@.    #  Return if called otherwise display current report
@0199:if <call> eq y  return . ; dsp,<cab>,<drw>,<rpt> .
@.
