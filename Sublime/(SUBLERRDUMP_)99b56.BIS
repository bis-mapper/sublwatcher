.Object Name: SUBLERRDUMP_
*    Version: 0001
*===================================================================================================
@. *** ERROR ENTRY POINT **********************************| *******************
@0001:.
:DEFINE RERDDRW_             "ecab$,I"                     | RUN ERROR DUMP DRAWER
:DEFINE RERDLOG_             "ecab$,I,1"                   | RUN ERROR DUMP LOG
:DEFINE FPATH_               "\\excaliber\is\blat-files\"  | EMAIL FILE PATH
:DEFINE GROWL_               "56,b,22"                     | EMAIL FILE PATH
@     ldv V198S256,V199S256 .
@     DIAG,RERDDRW_ A,,999  rnm -1 .                       | CREATE ERROR DUMP
@     lsm,xerr$ V199S256  ldv V199(81-0)='' .              | GET SYSTEM MESSAGE
@     ldv,l V199(081-12)=run$,\                            | ADD run NAME
            V199(093-05)=V198,\                            | PAGE OR JUST EMAIL?
            V199(098-04)=xrid$,\                           | RID
            V199(102-06)=xline$,\                          | LINE
            V199(108-04)=xfun$,\                           | COMMAND THAT FAILED
            V199(112-04)=xerr$,\                           | SYSTEM ERROR NUMBER
            V199(116-09)='0000     ',\                     | ERROR DUMP LOCATION
            V199(125-11)=user$,\                           | ACTION IS NOW USER
            V199(242-06)=date1$,V199(249-08)=time$ .       | ADD DATE AND TIME
@.                                                         |
@. *** SAVE TO ERROR DUMP LOG - ADD TO INDEX **************| *******************
@     dup,-1,RERDDRW_  ldv,r V199(116-4)=stat1$ .          | STORE ERROR DUMP
@     ldv,l V199(1-80),V198S256=¬V199(242-6)¬V199(81-12)\  | BUILD ERROR  log
'¬'V199(125-7)¬V199(249-8)¬V199(116-4)¬V199(98-4)¬\        | INDEX RECORD
V199(102-6)¬V199(108-4)¬V199(1-80)¬  ldv,c V199(1-80) .    | RECENTER
@     lok,RERDLOG_  ln+,RERDLOG_,,1,,V198  ulk .           | ADD TO ERROR INDEX
@.                                                         |
@. *** SCREEN MESSAGE *************************************| *******************
@     if orstan$ eq 0 . ; gto 0100 .                       | IF FOREGROUND
@     ldv <msg>s256='An error has occurred in 'RUN$' the details can',\
          <msg>=<msg>(p)' be found in report 'V199(116-4)'I56' .
@     call,GROWL_ 0001 ('Sublwatcher: Warning!','FAILED',<msg>) .
@.                                                         |
@. *** SEND EMAIL MESSAGE *********************************| *******************
@0100:if orstan$ eq 0  ldv V198S21=stnum$ . ; \            | UNIQUE NUMBER
                       ldv V198S21=orstan$ .               | FOR FILE NAME
@     rsl,-1  ln-,-0,46,9999999  ln-,-0,3,1 .  NO DIVIDER  | ONLY FIRST XX LINES
@     ldv,p <rpt>i4=V199(116-4) .
@     ln+,-0,1,1,,'.See full error dump '<rpt>'I'56 .
@     rnm -1 .
@     srh,2,e,2,,,0110 'dhr2-104' 'RUN NAME' ¬,'EMAILSUB' .
@     ldv,p <subject>s256='SUBLWATCHER RUN FAILED - 'RUN$ .
@     ldv,p <fp>s100=FPATH_ .
@     dc t2=t0(time$) <time>s6 \
      ldv,p <att>s80=<fp>'SUBLWATCHER DUMP-'date20$'-'<time>'.txt' .
@     fil,-1,n,n <att> .
@     ldv <to>s40='ed.winn@northstarmutual.com',<emtx>h5='email' .
@     br emailsub,run$,<to>,<subject>,,,,<att>,<emtx> .
@     wat 3000 .
@     os '-d -f' 'del "'<att>(p)'"' .
@.                                                         |
@. *** CLEAN UP changes.txt *******************************| *******************
@0110:fdr,SUBLCONFIG_ '' 'Parameter' ¬,'appdata'  rln 'value' <appdata>s .
@     fdr,SUBLCONFIG_ '' 'Parameter' ¬,'app'  rln 'value' <app>s .
@     ldv,p <appdir>s80=<appdata>(p)'\'<app>(p) .
@     pc,bcw "del <appdir>\site-netsit$\changes.txt" .
@.                                                         |
@. *** END PROGRAM - DISPLAY ERROR ************************| *******************
@     if orstan$ eq 0  ldv V198I6=2 . ; GTO END .          | BACKGROUND - END
@.
@. *** RESTART SUBLWATCHER ********************************| *******************
@     lsm,xerr$ v195s80  ldv,l v195 .
@     chg <err>i4 cerr$  lsm,<err> <v1>s88 .
@. @     sys .                                                                     system
@. @     fdr,-0,,,lin+2 '' 'USERID','RUNNAME' ¬,user$,'SUBLWATCHER' .              currently running?
@. @     rln 'STANUM' <station>s  kll,'SUBLWATCHER',<station> .                    kill it
@     run run$ .                                                         start file watcher utility
@.
@.                                                         |
@. *** RAR - ABORT ****************************************| *******************
@0196:chg <cerr>i4 cerr$ .
@.                                                         |
@. *** RER - EXIT  ****************************************| *******************
@0199:return .
