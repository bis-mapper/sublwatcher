.Import Category from a DOS Directory
*
*==============================================================================.
@     ldv <run>h1=y,<category>s20='ICE_MODULES_TEST',<status>h1,<format>h2=11 .
@     ldv <path>s256='\\excaliber\is\bis-files\site-E\' .
@     ldv <path>=<path>(p)'WINNE\Employee_Menu\ICE_Modules\' .
@0001:(<category>,<path>) .
@     ldv <format>h2=11,<status>h1 .
@     chg <ns>h1 CHR$254 .
@     srh,3950,d,1,,,lin+1 'd' 'category' ¬,<category>  gto lin+3 .
@     mbx,ok,E,'Cool ICE Admin - Export Category' \
      "Category (<category>(p)) does not exist."  O,(0199) ; .
@     rnm -5 .                                           Category details in -5
@     rar 0091 .                                         Register Abort Routine
@     fdr,-5 'a' 2-20 '¬',<category> .                    From Category details
@     rln 'Cab','Dw','Rpt','SvcCab','Sd','StyCab','St','StyRpt','CtgLoc','RW' \
      <toidxcab>a,<toidxdw>a,<toidxrpt>i,<toscab>a,<tosdw>a,\
      <tocstycab>a,<tocstydw>a,<tocstyrpt>i,<ctgloc>a,<rw>a .
@     ldv <frwidth>i6=80 .
@     fdr,-5,,,lin+1 '' 2-20,23-6 '*',<category>,Width  rln 30-6 <frwidth> .
@     if <rw> eq R  gto 0098 ; .                              Read Only Category
@0012:drw,<toscab>,<tosdw>,0093 <towidth>i3 .
@     if <frwidth> le <towidth> . ; gto 0090 .                      Check width
@.   ***************  Completion window  **************************************
@0015:ldv,pu <t1>s20=<category>,<t2>s20=<tocat> .
@.   **********  Import Services Table  ***************************************
@     if <format> eq 11 \
        ldv,p <file>s256=<path>(p)SVCTBL'.BIS' . ;\
        ldv,p <c>a4=<toidxcab>,<d>a1=<toidxdw>,<r>a4=<toidxrpt>,\
           <fileid>a9=<r><d><c>,<file>s85=<path>(p)SVCTBL<fileid>'.BIS' .
@     pcr,3950,D,-0,n,n,0092 <file>  rnm -1 .             Import Services Table
@     lzr,-1 ,<cc>i4  lch,-1,,lin+1 'af' 1-<cc> <ns>/¬  rnm -1 .
@     LFN,-1 2-20  <fld1>h18  LFN,-1 84-1  <fld2>h18 .
@     LFN,-1 86-1  <fld3>h18  LFN,-1 88-12 <fld4>h18 .
@     ldv,p <JS>h18=JS .
@     ldv,p <WI>h18=WI .
@     ldv,p <runcall>h18=RUNCALLED .
@     ldv,p <NEWFEILDS>s80=<WI>(p)','<JS>(p)','<runcall> .
@     if <fld1> ne 'ServiceName'  gto 0030  . ; . skip if not Services report
@     ldv,p <fldlist>s80=<fld2>(p)','<fld3>(p)','<fld4> .
@     if <fldlist> eq <NEWFEILDS>  gto 0030 . ; .
@     if <fldlist> ne '84-1,86-1,88-12'  gto 0102 . ; .
@0029:. To make importing category compatible with present version .
@     lzr,-1 ,,<hdr>i3 .
@     dec,2 <hdr>  wrl,-1,<hdr> 83-18 *,'.W.J.            .'
@     inc   <hdr>  wrl,-1,<hdr> 83-18 *,'.I.S.Run Called  .'
@     inc   <hdr>  wrl,-1,<hdr> 83-18 *,'.=.=.============.'
@0030:rsr 0265 .                                              verify format 3.0
@     ldv <trpt>i6=0,<nrpt>i6=0 .
@     srh,-1 'dn@l(*)' 'StyCab' '¬',@@@@ <vlines>i6 .       Nbr of Style Guides
@     inc,<vlines> <trpt> .
@     srh,-1 'dn@l(*)' 'Cab' '¬',@@@@ <vlines>i6 .       Nbr of Service Reports
@     inc,<vlines> <trpt> .
@     wrl,-1,2,y 29-20 '.',<tocat> .                      Update Services Table
@.   **********  Import Services  *********************************************
@0016:srh,-1,,,0018 'dn@l(*)' 'Cab' '¬',@@@@  rnm -2 .       Get Service Reports
@     ldv <first>a3=Yes,<useaspals>a3=Yes,<keepaspals>s40 .
@     ldv <aspals>s40,<aspnam>s30,<asppath>s132 .
@     fnd,-2 '' 2-1 '*',= ,<ln>i6 .
@0017:inc <ln> .
@     rdl,-2,<ln>,0018 'Service','Cab','Dw','Rpt','SvcTyp' \
      <service>s,<imscab>a,<imsdw>a,<imsrpt>a,<svctyp>h .
@     ldv <ext>h8=BIS  if <svctyp> eq DYN,STA,TMP,RPT,QDF . ; \
        ldv <ext>=<svctyp> .
@     ldv,p <imscab>,<imsrpt>,<ext> .
@     ldv,p <srvs>s20='('<service>(p)')' .
@     ldv,p <file>s256=<path>(p)<srvs>(p)<imsrpt><imsdw><imscab>'.'<ext> .
@     ldv <binary>a1=n .
@     fdr,3950,E,44,,,0089 '' 'Obj' '¬',<svctyp> .
@     rln 'Bn' <binary> .
@     ldv <impcab>a4=<toscab>,<impdw>a1=<tosdw> .
@     if <ctgloc> eq Rmt  ldv <impcab>a4=<xcab>,<impdw>a1=<xdrw> ; . Rmt Object?
@     if <svctyp> eq ASP  ldv <impcab>a4=0,<impdw>a1=F ; .           ASP Object?
@     pcr,<impcab>,<impdw>,-0,<binary>,n,0097 <file> .    Import Service Report
@     if <svctyp> eq ASP \                                           ASP Object?
        rnm -4 \                                      Save ASP object in -4 and
        rsl,3950,E,56 \                               Switch with dummy report
        ldv <objtitle>s40='Object Name: '<service> \
        wrl,-0,2,y 2-40 '.',<objtitle> ; .                   Insert Object name
@     rnm -3 .                                                    Service in -3
@     lzr,-3 ,<cc>i4  lch,-3,,lin+1 'af' 1-<cc> <ns>/¬  rnm -3 .
@     if <binary> eq y \
       if <svctyp> ne RPT \
         ldv <objtitle>s40='Object Name: '<service> \
         wrl,-3,2,y 2-40 '.',<objtitle> ; .
@     ldv,r <srpt>i4=<imsrpt> .                             Use original rpt nr
@     if <svctyp> eq ASP . ; gto 0021 .                             ASP Object?
@     gto 0160 .                                         Import ASP Script File
@0021:if <ctgloc> eq Rmt . ; gto 0022 .
@     lzr,-3 ,,,,,,,,,,<frrtyp>a1 .                                 Report type
@     if <frwidth> ne <towidth>  if <frrtyp> eq 1  gto 0088 ; .   Protected rpt
@     NWR,-3,<toscab>,<tosdw>,-0,0099 <vmsg>s80 .
@     ldv,p <t1>a4=<toscab>,<t2>a1=<tosdw>,<t4>a4=<imsrpt> .
@     NRN,099 "@rep,-0,<t1>,<t2>,<t4> ." <vmsg>s80 .  Replace in original rpt nr
@     gto 0023 .
@0022:.
@     rep,-3,<toscab>,<tosdw>,<srpt> .        Replace in original report number
@0023:fnd,-1 '' 'Service' '¬',<service> ,<lno>i6 .
@     wrl,-1,<lno>,y 'Cab','Dw','Rpt' '¬',<toscab>,<tosdw>,<srpt> .
@     gto 0017 .
@.   **********  Update Repository  *******************************************
@0018:dfu 3950,D,1,<toidxcab>,<toidxdw>,<toidxrpt> .            Lok repository
@     fnd,3950,D,1,,0091 '' 2-20 '¬',<category> ,<lno>i6 .
@     rep,-1,<toidxcab>,<toidxdw>,<toidxrpt> .
@     cmu .
@.   *******************  NORMAL RETURN  **************************************
@0019:ldv <status>=0 .
@     NOF .
@     if <run> eq y  dsp,ecab$,aedrw$,erpt$ . ; .
@     return .
@.   *******************  ERROR RETURN  ***************************************
@0091:ldv <status>=9 .
@     NOF .
@     if <CMP1> ne 0  CLS,<CMP1> ; .
@     ldv,w <cerr>i6=cerr$ car .                            Clear Abort Routine
@     return .
@.   ********************  IMPORT ASP SCRIPT OBJECT  **************************
@0160:ldv <aspals>s40,<aspnam>s30 .
@.   fdr,-1,,,lin2 '' 'Service',23-6 '*',<service>,AspAls .   ASP Alias Keyword
@.   rln 30-40 <aspals> .                                   ASP Directory Alias
@     fdr,-1,,,lin+2 '' 'Service',23-6 '*',<service>,AspNam . ASP File Name Keyw
@     rln 30-30 <aspnam> .                                        ASP File Name
@     if <first> eq Yes  ldv <first>=No  gto 0162 ; .
@     if <useaspals> eq Yes  ldv <aspals>=<keepaspals> ; .
@     fnd,3950,E,57,,lin+1 '' 'ASP Dir Alias','ASP File Name' \
       '¬',<aspals>,<aspnam>  gto 0162 .                      Does Object exist?
@.   *******************  Check ASP File  *************************************
@     fdr,3950,E,55,,,0162 '' 'Alias' '¬',<aspals> \
      rln 'Directory' <aspdir>s .
@     ldv,p <aspdir>,<taspnam>s30=<aspnam>,<asppath>s132=<aspdir><taspnam> .
@     ret,0,F,b,n,0164 <asppath>,,,1 .                     Does ASP File exist?
@     ldv,pu <t1>s30=<aspnam>,<t2>s40=<aspals> .
@     MBX,yn,Q,'Cool ICE Admin - Import ASP Object' \
"ASP file name <t1> in alias <t2> exist.//Overwrite?" Y,(0164),N,(lin+1) .
@0162:call,edrw$,097 0001 ('Import',<aspals>,<aspnam>,<asppath>s132,\
      <useaspals>,<RWIN>,<status>) .                       Ask User what to do?
@     if <useaspals> eq Yes  ldv <keepaspals>=<aspals> ; .
@     if <status> eq 9  gto 0091 ; .                                      Cancel
@.   *******************  Write ASP Script File  ******************************
@0164:ln+,-4,1,1  wrl,-4,2 2-5 '$',DATA$ .                    Keep Tab character
@     fil,-4,n,n,0095 <asppath> .
@     fnd,-1,,lin+2 '' 'Service',23-6 '*',<service>,AspAls ,<lno>i6 . ASP Alia
@     wrl,-1,<lno> 30-40 '*',<aspals> .              Update ASP Directory Alias
@     fnd,-1,,lin+2 '' 'Service',23-6 '*',<service>,AspNam ,<lno>i6 . ASP File
@     wrl,-1,<lno> 30-30 '*',<aspnam> .                           ASP File Name
@     lok,3950,E,57 .                                 Update ASP Object Table
@     fnd,3950,E,57,,lin+1 '' 'Category','Service' \
                             '¬',<tocat>,<service> ,<lno>i6  gto lin+2 .
@     lzr,3950,E,57 <lno>i6  ln+,3950,E,57,<lno>,1  inc <lno> .
@     wrl,3950,E,57,<lno> \
         'ASP Dir Alias','ASP File Name','Category','Service' \
         '¬',<aspals>,<aspnam>,<tocat>,<service> .
@     ulk .
@     gto 0021 .
@.   *******************  Error Messages  *************************************
@0087:ldv,pw <t1>a6=<towidth>,<t2>h1=tic$ .
@     MBX,ok,E,'Cool ICE Admin - Import Category' \
"Drawer with a width greater than <t1> is not available for temporary \
storage.//\
Allocate a drawer with a width of at least <t1> using the <t2>Allocate \
Work Drawer<t2> function." O,(0091) .
@0088:ldv,pu <t1>s20=<service>,<t2>a6=<towidth> .
@     srh,-1 'dpn' 'Service' '¬',<service>  rnm -1 .   Remove from Service Table
@     MBX,ok,E,'Cool ICE Admin - Import Category' \
"Cannot import object <t1> to remote system.//A work drawer with a width of \
<t2> characters is required to transfer binary objects to this remote \
category.//Importing this object will be skipped." O,(027) .
@0089:ldv,pu <t1>s20=<service> .
@     srh,-1 'dpn' 'Service' '¬',<service>  rnm -1 .   Remove from Service Table
@     MBX,ok,E,'Cool ICE Admin - Import Category' \
     "Object type <svctyp> is not defined in the object table.//\
Importing object <t1> will be skipped." O,(027) .
@0090:MBX,yn,Q,'Cool ICE Admin - Import Category' \
     "Width of the category importing into is smaller than the original.//\
Objects may be truncated if you continue.//\
Do you want to continue?" N,(0091),Y,(0015) .
@0092:ldv,pu <t1>s85=<file> .
@     MBX,ok,E,'Cool ICE Admin - Import Category' \
     "File <t1> was not found on your workstation.//\
Make sure the directory contain Cool ICE exported objects." O,(0091) .
@0093:ldv,pu <t1>s20=<tocat> .
@     ldv,p <toscab>,<tdc>a5=<tosdw><scab> .
@     MBX,ok,E,'Cool ICE Admin - Import Category' \
"Drawer <tdc> containing objects for category <t1> does not exist." O,(0091) .
@0094:ldv,pu <t1>s20=<tocat> .
@     MBX,ok,E,'Cool ICE Admin - Import Category' \
"Keyword RMTSVR containing server specifications for \
remote Category <t1> is missing." O,(0091) .
@0095:ldv <stat2>i6=stat2$ .
@     lsm,<stat2> <msg>s80 .
@     MBX,ok,E,'Cool ICE Admin - Import Category' \
"Importing of ASP using the aspals <aspals>(p) causes @FIL error:/\
<msg>(p)/\
The erroring path is:/\
<asppath>(p)" O,(0162) .
@0096:ldv,pu <t1>s85=<file> .
@     MBX,ok,E,'Cool ICE Admin - Import Category' \
     "File <t1> could not be read.//\
Make sure the directory contain Cool ICE exported objects." O,(0091) .
@0097:ldv,pu <t1>s85=<file> .
@     srh,-1 'dpn' 'Service' '¬',<service>  rnm -1 .   Remove from Service Table
@     MBX,ok,E,'Cool ICE Admin - Import Category' \
     "File <t1> could not be read.//\
Importing this object will be skipped." O,(027) .
@0098:ldv,pu <t1>s20=<tocat> .
@     MBX,ok,E,'Cool ICE Admin - Import Category' \
"Category <t1> is a read only category." O,(0091) .
@0101:ldv <t2>h1=tic$ .
@     ldv,p <xcab>,<tdc>a5=<xdrw><xcab> .
@     MBX,ok,E,'Cool ICE Admin - Open Category' \
"Work drawer <tdc> does not exist.//\
Allocate a work drawer using <t2>Allocate Work Drawer<t2> function." O,(0091) .
@0102:MBX,YN,E,'Warning' "Service Table contains unexpected fields in \
columns 84-16. These fields will be overwritten. Import Services?\
"Y,(0029),N,(0091) .
@.   *******************  SERVER ERROR RETURN  ********************************
@0099:MBX,ok,E,'Cool ICE Admin - Connecting to Remote Cool ICE Server' \
     "The following message was returned from the remote Cool ICE server:-//\
<vmsg>" O,(0091) .
@.   *********** Reformat Object Table for 3.0 or later ***********************
@0265:.
@     rdl,-1,4 34-9 <hdr>h .
@     if <hdr> ne '.Typ.ess.'  esr . ; .                             old format?
@     brk .
@     rdc,3950,e,13,2,4 1-256 <data>s .                        Object template
<data>
@     ldv <ln>i5=5 .
@0267:inc <ln>  rdl,-1,<ln>,0269 1-132 <data>s .
@     if <data>(1-1) eq '¬'  gto 0268 .
<data>
@     gto 0267 .
@0268:.
<data>(1-37)     <data>(38-94) .
@     gto 0267 .
@0269:brk  rnm -1 .
@     esr .
@.   **********  Reformat Style Guide for 2.0 and older  **********************
@0275:dvs,-0,0278 'Siz' <sz>s .                       Is reformatting required?
@     ldv <hd>s118=Value,<hd>(0-1)='.' .
@     ldv <eq>s118  lcv '' <eq> ' '/=  ldv <eq>(0-1)='.' .
@     fnd,-0 '' 2-1 '*',= ,<lno>i6 .
@0276:inc <lno> .
@     rdl,-0,<lno>,0277 'Keyword','Value','Description' <t1>s,<t2>s,<t3>s .
@     ldv <t4>s117=<t2> .
@     ldv <t5>s131=<t1>' Descr ='<t3> .
@     wrl,-0,<lno>,y 15-117 '¬',<t4> .
@     ln+,-0,<lno>,1  inc <lno> .
@     wrl,-0,<lno>,y 2-132 '*',<t5> .
@     gto 0276 .
@0277:wrl,-0,4,y 15-118 '*',<hd>/'*',<eq> .
@0278:esr .
