.Object Name: SUBLWATCHER
.
.
.
*
*
*===============================================================================
:INCLUDE,ecab$,B,1 .                                                            INCLUDE
@0001:.
@     SUBLRER_ .
@     rar 0198 .                                                                Abort label
@     brk  brk,0,I  rnm -2 .                                                    Clear Attrib Result
@     brk  brk,0,I  rnm -3 .                                                    Read Attrib Result
@.
@.    #  Read config table to load in run variables
@     rsr,SUBLCOMMON_ configVars_ .
@.
@.    #  Get application path... If first time run setup
@     call,SUBLCOMMON_ appPath_ (<user>,<apppath>s256) .
@     call,SUBLCOMMON_ getChgsFile_ (<appdir>,<chgsfile>s256) .
@.
@.    #  Compare version of user to version of SUBLWATCHER.
@     call,SUBLCOMMON_ readConfigInf_ ('CONFIG','version',<cfgver>s100) .
@     ldv,p <cfgver> .
@     if <cfgver> eq ''  gto 0004 . ; .
@     call,SUBLCOMMON_ parseVersion_ (<ver>,<major>h16,<minor>h16,<patch>h16) .
@     call,SUBLCOMMON_ parseVersion_ (<cfgver>,<cfgmajor>h16,<cfgminor>h16,<cfgpatch>h16) .
@     if <cfgmajor> eq <major> \
        if <cfgminor> eq <minor> \
          if <cfgpatch> eq <patch>  gto 0005 . ; .
@0004:call,SUBLSETUP_ 0101 () .
@.
@0005:ldv <dcnt>i2=-1
@.
@.    #  Set up animation
@0010:inc <cnt>,<dcnt> .
@     ldv <dspc>h3='' .
@     if <dcnt> gt 3  ldv <dcnt>=0 .
@     HSH <random>I3=MSEC$(6-0),1-100 .
@     if <random> eq 47,49,51,53 ldv <eye>h3='---' . ; ldv <eye>h3='(-)' .
@     if <dcnt> eq 0 . ; lcv,lin1 '' <dspc>(1-<dcnt>) ' '/'.' .
@     if <cnt> gt 18  ldv <cnt>i2=1  .
@     ldv <dsp>s19='                  |' .
@     lcv,lin1 '' <dsp>(1-<cnt>) ' '/']' .
@     brk .

        , , , , ,
      /`-`-`-`-`-`\      Program: <thisrun>
     /             \     Version: <ver>
    /  ~~~     ~~~  \       User: user$
    | _____   _____ |       Site: netsit$
   (|  <eye>  ^  <eye>  |)
    |        \      |    .------------------.
    \       ._)     /    |<dsp>(p)
     \ .         . /     +------------------+
      \ `-------` /
       \   ...   /       WATCHING FOR CHANGES
        `\`;`;`/`        <chgsfile>
          `\;/`

@.
@.                                      MMMMMMMMMMMMMMMMMMM.
@.                                  MMMMMMMM===========MMMMMMMM.
@.                               MMMMMM=====================MMMMMM
@.                             MMMMM===========================MMMMM
@.                           MMMM=================================MMMM
@.                         .MMM=====================================MMM.
@.                        MMMM======================================MMMMM
@.                       MMM==========================================MMMM
@.                      MMM===MMMMMM====================MMMMMM==========MMM
@.                     MMM==MMM    MMM===============MMMM    MMMM========MMM
@.                    MMM==MM     MMMMM=============MM      MMMMMM========MMM
@.                    MMM=MM     MMMMMMM===========MM       MMMMMMM========MM.
@.                   MMM=MM       MMMMMMM=========MM         MMMMMMM=======MMM
@.                  .MM==MM            MM=========MM              MM========MM.
@.                  MMM==MM            MM=========MM              MM========MMM
@.                  MMM==MM.           MM=========MM              MM========MMM
@.                  MM===MMMMMMMMMMMMMMMM=========MMMMMMMMMMMMMMMMMM=========MM.
@.                  MM=======================================================MM.
@.                  MM=======================================================MM.
@.                  MM=======================================================MM.
@.                  MM=====MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM===========MM.
@.                  MMM=====MMM                                  MM=========MMM
@.                  MMM======MM                                   M=========MMM
@.                  .MM======MM                                   MM========MM.
@.                   MMM======MM                                  MM=======MMM
@.                   .MMM=====MM                                  M=======MMM
@.                    MMM======MM                                MM======MMMM
@.                     MMM======MM               ::::::         MM=======MMM
@.                      MMM======MM           ::::::::::::    MMM=======MMM
@.                       MMM======MMM       :::::::::::::::  MMM=======MMM.
@.                        MMMMM====MMM     :::::::::::::::MMMM=======MMMM
@.                          MMMM====MMM   ::::::::::::::MMMM=======MMMM.
@.                           MMMMM====MMMMM:::::::::MMMMM========MMMMM
@.                             MMMMM======MMMMMMMMMMMMM========MMMMM
@.                               MMMMMM=====================MMMMMM
@.                                  MMMMMMMMM=========MMMMMMMMM.
@.                                      MMMMMMMMMMMMMMMMMMM
@.
@     brk .
@     ldv,p <thisrun>h11=RUN$ .
@     dsp,-0,,,,Y,,' '<thisrun>' is watching'<dspc> .
@.
@.    #  Watch for chgs file
@0034:dbg,,w,<delay>(p) .
@     pcr,0,I,-0,n,n,0010 <chgsfile> .                                          File Exist?
@     rnm -5  lzr,-5 <lns>i7,<icc>i4,<lnh>i7 .
@     if <lns> gt <lnh> . ; gto 0010 .                                          Look for changes

@.    #  Get field sizes for logging
@     dvs,SUBLOG_ 'Event','File','Object','Input' \
        <event>s,<file>s,<obj>s,<input>s .
@.
@0036:inc <lnh>  rdl,-5,<lnh>,0070 1-256 <data>s .                              Get files
@     ldv,ql <event>=<data>,0(,),\
             <file>=<data>,1(,),\
             <obj>=<data>,2(,),\
             <input>s80=<data>,3(,),\
             <inp1>s40=<data>,4(,),\
             <inp2>s40=<data>,5(,),\
             <inp3>s40=<data>,6(,),\
             <inp4>s40=<data>,7(,),\
             <inp5>s40=<data>,8(,) .
@.
@.    #  Git Hooks Section
@.    #  Will take hook result and convert it into CSV format
@.    #  Give user option to load programs or skip
@.    #  Status 0 eq loop back up and load programs
@.    #  Status 9 cancel action and delete changes file
@     rdl,-5,2 1-11 <gitinput>s .
@     if <gitinput> eq 'BISGITHOOKS' . ; gto 0037 .
@     call,SUBLGITHOOKS_ 0001 (<status>h1) .
@     if <status> eq 0  rnm -5  lzr,-5 <lns>i7,,<lnh>i7  gto 0036 . ; gto 0070 .
@.
@.    #  Start BisCmd
@0037:ldv <biscmd>h1=n .
@     if <event> eq 'BisCmd' . ; gto 0040 .
@     ldv <biscmd>h1=y .
@     if <inp1> gt '' . ; gto 0038 .
@     if link$ eq 0 . ; clk .
@     call,BISCMD_ 0002 (<file>,<input>,<inp1>,<inp2>,<inp3>,<inp4>,<inp5>,<status>h1) .
@     if <status> eq 9,(0036) . ; gto 0040 .
@.
@0038:call,BISCMD_ 0001 (<file>,<input>,<status>h1) .
@     if <status> eq 9,(0036) .
@.
@.    #  Make sure were on the correct site for this file...
@0040:lcv,0042 'b1' <file> 'site-' <pos>i3 .                                    Find string
@     inc,5 <pos>  ldv <sl>h1=<file>(<pos>-1) .                                 Read Site Letter from file
@     if <sl> ne netsit$ . ; gto 0042 .                                         Compare
@     ldv <msg>s256='Site letters differ... Cannot save file.' .
@     call,GROWL_ 0001 ('Sublwatcher: Warning!','FAILED',<msg>) .
@     gto 0036 .
@.
@0042:.
@     call,SUBLCOMMON_ getFileName_ (<file>,<run>s60) .                         Extracts File Name from path
@.
@.    #  Only .bis files are allowed past this point
@     lcv,0036 'b99' <run> '.bis'/'' .                                          Remove file type
@     lcv,lin+1 'b99' <run> '.html'/'' .                                        Remove file type
@     ldv,p <run> .
@.
@.    #  Load run location
@     call,SUBLCOMMON_ getCabDrwRpt_ (<run>,<cab>i4,<drw>h1,<rpt>i4) .
@.
@.    #  Logging
@     if <input> eq '<input>'  ldv <input>s0 . ; .
@     DFU SUBLOG_ .
@     ln+,SUBLOG_,,1  ldv <wln>i7=stat1$ .
@     wrl,SUBLOG_,<wln>,Y 'Event','Report','User','Date','Time','File','Object','Input' \
        ¬,<event>,<run>,user$,date11$,time$,<file>,<obj>,<input>(p) .
@     cmu .
@.
@     if <biscmd> eq 'y',(0036) .                                               Wait for next event
@.
@.    #  Make sure its a valid MAPPER report
@     ldv <vm>i1=0 .
@     def <t>i2,<cab>  if <t> eq 1  inc <vm> .                                  Numeric
@     def <t>i2,<rpt>  if <t> eq 1  inc <vm> .                                  Numeric
@     def <t>i2,<drw>  if <t> eq 2  inc <vm> .                                  Alpha
@     if <vm> eq 3,(0047) .                                                     Valid
@     pc,bnw "del <chgsfile>(p)" .                                              Clean up file
@.
@     ldv <msg>s80=<file>': Is not a valid MAPPER report' .
@     call,GROWL_ 0001 ('Sublwatcher: Warning!','FAILED',<msg>) .
@     gto 0036 .
@.
@.    #  Retrieve the file and clear attribute again
@0047:if <input> eq 'git-hook'  if <event> eq 'deleted'  gto 0049 . ; .
@     pcr,0,I,-0,n,n,0034 <file>  gto 0048 .
@     if stat1$ eq 1 \
        ldv <msg>s80='The file has been deleted' .
@     if stat1$ eq 2 \
        ldv <msg>s80='Binary files are not supported' .
@     pc,bnw "del <chgsfile>(p)" .                                              Clean up file
@.ew  MBX,OK,S,'Sublwatcher: Warning!' "<msg>(p)" O,(0036) .
@     call,GROWL_ 0001 ('Sublwatcher: Warning!','FAILED',<msg>) .
@     gto 0036 .
@.
@0048:rnm -4 .
@     pc,bnw "del <chgsfile>(p)" .                                              Clean up file
@.
@.    #  Make sure were not editing this file
@     if <cab> eq ecab$  if <drw> eq aedrw$  if <rpt> eq erpt$,(0036) . ; .     Cannot modify itself
@.
@.    #=============================================================================================
@.    #===   CHECK IF RID IS LOCKED FOR EITHER READ OR WRITE, IF SO PROMPT BEFORE CONTINUE   =======
@0049:lzr,<cab>,<drw>,<rpt> ,,,,,,,<vrpw>h6,<vwpw>h6 .
@     if 'LOCKED' eq <vrpw>,<vwpw> . ; gto 0051 .
@     ldv <msg>s256='This rid contains either a read or write password. ',\
          <msg>=<msg>(p)' Waiting for input...' .
@     call,GROWL_ 0001 ('Sublwatcher: Warning!','WARNING',<msg>) .
@     mbx,YN,Q,'Sublwatcher: Whoa there!' \
"Would you like to continue working with protected rid <rpt>(p)<drw>(p)<cab>(p)? //" Y,(lin+1),N,(0036) .
@.
@.    #=============================================================================================
@.    #===   CHECK IF REPORT 0, GIVE OPTION TO REPLACE   ===========================================
@0051:if <rpt>(p) eq 0 . ; gto 0053 .
@     ldv,p <canchg>s200='You must be signed in under department 2 in order to change report 0.' .
@     ldv <msg>s256='Attempt to save over report 0.',\
          <msg>=<msg>(p)'Waiting for input...' .
@     call,GROWL_ 0001 ('Sublwatcher: Warning!','WARNING',<msg>) .
@     mbx,YN,Q,'Sublwatcher: Whoa there!' \
"Would you like to replace <rpt>(p)<drw>(p)<cab>(p)? //" Y,(lin+1),N,(0036) .
@     net,,netsit$,mapcoord,2,nslock .
@     nwr,-4,0,i,-0 .
@     nrn "@call,SUBLRPTZERO_ 0fd001 ('<cab>','<drw>','<rpt>') . " .
@     nof .
@     gto 0036 .
@.
@.    #=============================================================================================
@.    #=== CHECK IF NEW MAPPER RID, ALLOW RID TO BE CREATED   ======================================
@0053:if <input> eq 'git-hook'  if <event> eq 'added'  gto 0061 . ; .
@     lzr,<cab>,<drw>,<rpt>,lin+1  ldv <exists>h1='Y'  gto 0055 .
@     ldv <exists>h1='N' .
@     ldv <msg>s256='This file does not exist in Mapper.',\
          <msg>=<msg>(p)'Waiting for input...' .
@     call,GROWL_ 0001 ('Sublwatcher: Warning!','WARNING',<msg>) .
@     mbx,YN,Q,'Sublwatcher: Whoa there!' \
"This run does not currently exist! //\
 Run:   <rpt>(p)<drw>(p)<cab>(p) //\
 Would you like to create it? //" Y,(lin+1),N,(0036) .
@     rep,-4,<cab>,<drw>,<rpt> .
@     ldv <msg>s256=<rpt>(p)<drw>(p)<cab>(p)' was created.' .
@     call,GROWL_ 0001 ('Sublwatcher: Success!','SUCCESS',<msg>) .
@     gto 0056 .
@.
@.    #=============================================================================================
@.    #=== CHECK IF USER HAS UPDATE CONTROL, ALLOW TO TAKE CONTROL   ===============================
@0055:rdl,<cab>,<drw>,<rpt>,1 53-11 <controluser>s .
@     if user$ ne <controluser> . ; gto 0056 .
@     ldv <msg>s256='You do not have update control of this report.',\
          <msg>=<msg>(p)'Waiting for input...' .
@     call,GROWL_ 0001 ('Sublwatcher: Action Needed!','PENDING',<msg>) .
@     mbx,YNC,Q,'Sublwatcher: Whoa there!' \
"You do not have update control of this report. //\
 Site:   netsit$    Run:   <rpt>(p)<drw>(p)<cab>(p)    Name:   <controluser>(p)  //\
 (Press CANCEL to perform a DIFF on the file)  //\
 Would you like to take control of this report?//" Y,(lin+1),N,(0036),C,(0057) .
@0056:lzr,3950,d,1,0058 .                                                       ICE Enabled Site?
@     srh,3950,d,1,,,0058 'ad' 'SvcCab','sd' ¬,<cab>,<drw> .
@     lzr,-0 ,,<hln>i7  inc <hln> .
@     rdl,-0,<hln>,lin+1 'cab','dw','rpt' <icab>h,<idw>h,<irpt>h .
@     fdr,<icab>,<idw>,<irpt>,,,0058 '' 'cab','dw','rpt' ¬,<cab>,<drw>,<rpt> .
@     rln,,lin+1 'service' <srvs>s  gto 0058 .
@     call,GROWL_ 0001 ('Sublwatcher: Warning!','FAILED','Check input message') .
@     mbx,YN,Q,'What are we doing?' \
"Do you want to add this as an ice service//" Y,(0019),N,(0061) .
@     gto 0058 .
@0057:
@     ldv,p <diffCmd>s40='DIFF '<rpt>(p)<drw>(p)<cab>(p)
@     call,BISCMD_ 0001 (<file>,<diffCmd>,<status>h1) .
@     gto 0055 .
@.
@.    #  Skip ahead for git-hooks
@0058:if <input> eq 'git-hook'  if <event> eq 'deleted'  gto 0063 . ; .
@.
@.    #=============================================================================================
@.    #===   NOTIFICATION FOR UPDATES OUT OF REPORT BOUNDS!   ======================================
@     lzr,<cab>,<drw>,<rpt> ,<tmax>i3  lzr,-4 <4nl>i6,,<4hl>i6  .
@     if <tmax> ge 980 gto 0061 . ; .
@     inc <tmax>  ldv <bmax>i3=<tmax>  inc,18 <tmax>  .
@.    #---   Test for heading divider line (*=) - Usually BINARY if none   -----
@     fnd,-4,,0061 '' 2-1 *,'=' .
@.
@     cal,-4,<4hl>,<4nl> 'acl' <bmax>-18 ,a if:LIT(a)<>"                  ";
@     lzr,-0 <0lns>i6,,<0hln>i6  if <0lns> gt <0hln> . ; gto 0061 .
@     pcw,-0 <datadir>'\ExceededBoundaries.txt' .   TESTING WHAT IS OUTPUT
@     ldv <msg>s150='Warning: updates outside report boundaries have been discarded.',\
          <msg>=<msg>(p)'See ExceededBoundaries.txt for which lines are being cut off.' .
@     call,GROWL_ 0001 ('Sublwatcher: Warning!','WARNING',<msg>) .
@.
@.
@.
@.    #  Load the ¬ character and handle the conversion
@0061:.
@     rsl,-4 .
@     call,SUBLCOMMON_ charReplace_ ('import') .                                Replace tab character
@     rnm -4 .
@.
@     lok,<cab>,<drw>,<rpt>,lin+1  gto 0063 .
@     ldv <msg>s80='Updating of report '<rpt>(p)<drw>(p)<cab>(p)' is in progress...' .
@     call,GROWL_ 0001 ('Sublwatcher: Warning!','FAILED',<msg>) .
@     gto 0036 .
@.
@.
@0063:if <event> eq 'deleted' . ; gto 0064 .
@     dlr,<cab>,<drw>,<rpt> .
@     ulk .
@     ldv <msg>s80='File: '<rpt>(p)<drw>(p)<cab>(p)' deleted. Site: 'SITE$ .
@     call,GROWL_ 0001 ('File Deleted','DELETED',<msg>) .
@     gto 0036 .
@.
@0064:rep,-4,<cab>,<drw>,<rpt> .
@     ulk .
@.
@     if <event> eq 'added' . ; gto lin+4 .
@     ldv <msg>s80='NEW File: '<rpt>(p)<drw>(p)<cab>(p)' added. Site: 'SITE$ .
@     call,GROWL_ 0001 ('New File Saved','SUCCESS',<msg>) .
@     gto 0036 .
@.
@     ldv <msg>s80='File: '<rpt>(p)<drw>(p)<cab>(p)' saved. Site: 'SITE$ .
@     call,GROWL_ 0001 ('File Saved','SUCCESS',<msg>) .
@     gto 0036 .
@.
@.    #  Start over and keep re-checking
@0066:if ws$ eq 0,(lin+2) .
@     if <exists> eq y  run V,<rpt>,<drw>,<cab> . ; .
@     GTO END .
@.
@.    #  Start over and keep re-checking
@0070:pcr,0,I,-0,n,n,lin+2 <chgsfile>(p) .                                      File Exist
@     pc,bnw "del <chgsfile>(p)" .                                              Clean it up
@     gto 0010 .
@.
@.    #  Run was aborted
@0198:.
@     rel .
@.
