.Display_MessageBox   Display Message Box
.REV: 4.6.0  DATE: 20121212
*===============================================================================
*  RADS - Rapid Application Development Studio                                 *
*  Copyright (c) 2012 IT Resource Solutions (ITRS)                             *
*  Execution of this program without a valid software license, or reuse        *
*  of this program code without expressed consent of ITRS, is prohibited.      *
*==============================================================================*
:L1=21,2=49,3=59,4=61,11=83,12=85,13=87,196=92,199=97
@.
@. ------------------------------- Test Setup ----------------------------------
@.
@     LDA <P>h12[3]=1D0,MAPCOORD,123 .
@     CALL 0001('REPORTLOCKED',<P>,<RSP>h1) .
<RSP>
@     GTO END .
@.
@. ---------------------------- Call Entry Point -------------------------------
@.
@0001:(<ERR>,<P>,<RSP>) .
@.
@     IF DRW$ ne 0  RNM -1 ; .
@.
@     ERROR_ .
@.
@     LDA <M>s80[9] .
@     CHG <CR>h1 <CR> - 19 .
@.
@     LDV <PT>h1=S  LDV,p <PX>s80=<P> .
@     DEF,q <PQ>i2,<P>  IF <PQ> gt 0  LDV <PT>=A  GTO lin+3 ; .
@     IF <P> ne ''  LCV,lin+1 n <P> '¬' ; GTO lin+2 .
@     LCV b99 <P> '¬' ,<PQ>  LDV <PT>=T .
@.
@     LDV <TYP>h3=OK,<ICN>h1,<CAX>s24=<ERR> .
@     SRH,ECAB$,C,6,,,lin+2 dq 'ErrorID' ¬,<ERR>  RNM -2 .
@     RDL,-2,6 'Typ','Ic','Caption' <TYP>h,<ICN>h,<CAX>s .
@     LDV <CAP>s80=<CAX> .
@     LCV,lin+8 '' <CAP> '%'  IF <PT> eq 'A',(lin+2),'T',(lin+5) ; .
@     LCV '' <CAP> '%1'/<PX>  GTO lin+7 .
@     CHG <PN>i2 <PQ> + 1 .
@     DEC <PN>  IF <PN> lt 1,(lin+5) ; LDV,p <PX>s80=<P>[<PN>] .
@     LDV,p <PP>a2=<PN>  LCV,lin-1 '' <CAP> '%'<PP>/<PX>  GTO lin-1 .
@     LDV <PN>i2=<PQ> .
@     IF <PN> lt 0,(lin+2) ; LDV,qp <PX>s80=<P>,<PN>  CHG <PP>a2 <PN> + 1 .
@     DEC <PN>  LDV,p <PP>  LCV,lin-1 '' <CAP> '%'<PP>/<PX>  GTO lin-1 .
@.
@     IF DRW2$ eq 0,(0003) ; RDL,-2,6 1-80  LDV <N>i1=0 .
@0002:INC <N>  IF <N> gt 9,(0004) ; RLN,,0004 1-80 <M>[<N>] .
@     LDV <M>[<N>](1-1)=<CR> .
@     LCV,0002 '' <M>[<N>] '%'  IF <PT> eq 'A',(lin+2),'T',(lin+5) ; .
@     LCV '' <M>[<N>] '%1'/<PX>  GTO 0002 .
@     CHG <PN>i2 <PQ> + 1 .
@     DEC <PN>  IF <PN> lt 1,(0002) ; LDV,p <PX>s80=<P>[<PN>] .
@     LDV,p <PP>a2=<PN>  LCV,lin-1 '' <M>[<N>] '%'<PP>/<PX>  GTO lin-1 .
@     LDV <PN>i2=<PQ> .
@     IF <PN> lt 0,(0002) ; LDV,qp <PX>s80=<P>,<PN>  CHG <PP>a2 <PN> + 1 .
@     DEC <PN>  LDV,p <PP>  LCV,lin-1 '' <M>[<N>] '%'<PP>/<PX>  GTO lin-1 .
@0003:.
@     LDV <M>[1]='Undocumented Message' .
@0004:.
@     IF <M>[9](2-0) ne '',(lin+8) ; LDV <M>[9]='' .
@     IF <M>[8](2-0) ne '',(lin+7) ; LDV <M>[8]='' .
@     IF <M>[7](2-0) ne '',(lin+6) ; LDV <M>[7]='' .
@     IF <M>[6](2-0) ne '',(lin+5) ; LDV <M>[6]='' .
@     IF <M>[5](2-0) ne '',(lin+4) ; LDV <M>[5]='' .
@     IF <M>[4](2-0) ne '',(lin+3) ; LDV <M>[4]='' .
@     IF <M>[3](2-0) ne '',(lin+2) ; LDV <M>[3]='' .
@     IF <M>[2](2-0) ne '',(lin+1) ; LDV <M>[2]='' .
@     LDV,p <M>[1],<M>[2],<M>[3],<M>[4],<M>[5],<M>[6],<M>[7],<M>[8],<M>[9] .
@     LCV '' <M>[1] <CR>/ .
@     LDV <1>h1=<TYP>(1-1),<2>h1=<TYP>(2-1),<3>h1=<TYP>(3-1) .
@     IF <TYP> eq 'CO'  LDV <TYP>=OC ; .
@     IF <TYP> eq 'NY'  LDV <TYP>=YN ; .
@     IF <TYP> eq 'YCN','NYC','NCY','CYN','CNY'  LDV <TYP>=YNC ; .
@     IF <TYP> eq 'CR'  LDV <TYP>=RC ; .
@     IF <TYP> eq 'AIR','RAI','RIA','IAR','IRA'  LDV <TYP>=ARI ; .
@     LDV,p <TYP>,<ICN>,<CAP> .
@     MBX,<TYP>,<ICN>,<CAP> \
      "<M>[1]<M>[2]<M>[3]<M>[4]<M>[5]<M>[6]<M>[7]<M>[8]<M>[9]" \
      <1>,(0011),<2>,(0012),<3>,(0013) .
@     GTO 0199 .
@0011:.
@     LDV <RSP>h1=<1>  GTO 0199 .
@0012:.
@     LDV <RSP>h1=<2>  GTO 0199 .
@0013:.
@     LDV <RSP>h1=<3>  GTO 0199 .
@.
@. ------------------------------- Termination ---------------------------------
@.
@0196:. Abort routine
@.
@     CHG <CERR>i4 CERR$ .
@     GTO 0199 .
@.
@0199:. Return
@.
@     IF DRW1$ ne 0  RNM,-1 -0 . ; RSL,EDRW$,0001  DLR,-0 .
@     RETURN .
