.SUBLWATCHER
.
.
.
*
*
*===============================================================================
:INCLUDE,ECAB$,B,1 .                                                            INCLUDE
@0001:.
@     RAR 0198 .                                                                Abort label
@     RER,0,E,2  0001 .
@     BRK  BRK,0,I  RNM -2 .                                                    Clear Attrib Result
@     BRK  BRK,0,I  RNM -3 .                                                    Read Attrib Result
@.
@.    #  Read config table to load in run variables
@     RSR,SUBLCOMMON_ configVars_ .
@.
@.    #  Get field sizes for logging
@     DVS,SUBLOG_ 'Event','Report','File','Object' \
        <EXX>h,<RXX>h,<FXX>s,<OXX>h .
@     DEF,s <ESZ>i3,<EXX> .
@     DEF,s <RSZ>i3,<RXX> .
@     DEF,s <FSZ>i3,<FXX> .
@     DEF,s <OSZ>i3,<OXX> .
@.
@.    #  Get application path... If first time run setup
@     CALL,SUBLCOMMON_ appPath_ (<USER>,<APPPATH>s256) .
@     CALL,SUBLCOMMON_ getChgsFile_ (<APPDIR>,<CHGSFILE>s256) .
@.
@.    #  Start watchmedo.exe to watch directories
@0005:PC,bc 'cmd /C '"<BATFILE>" .
@.
@.    #  Set up animation
@0010:INC <CNT> .
@     LDV <PRUN>s0 .
@     IF <CNT> gt 9  LDV <CNT>i2=1  .
@     LDV <DSP>h11='          /' .
@     LCV,lin+1 '' <DSP>(1-<CNT>) ' '/'=' .
@     BRK .

    /               \
   /  ~~~~     ~~~~  \    SUBL WATCHER
  /  ______   ______  \   Version: <VER>
  \    (-)  ^  (-)    /
   \       /^\       /
    \     /_^_\     /
     \             /
      \ <DSP>(p)
       \    =    /
         \ \ / /
         \ \ / /

  Waiting for changes
  <CHGSFILE>

@.
@     BRK .
@     DSP,-0,,,,Y,,' SUBLWATCHER ' 'DATE11$' 'TIME$' .
@.
@.    #  Watch for chgs file
@0034:dbg,,w,<DELAY>(p) .
@     PCR,0,I,-0,n,n,0010 <CHGSFILE> .                                          File Exist?
@     RNM -5  LZR,-5 <LNS>i7,,<LNH>i7 .
@     IF <LNS> gt <LNH> . ; GTO 0010 .                                          Look for changes
@0036:INC <LNH>  RDL,-5,<LNH>,0070 1-256 <DATA>s .                              Get files
@     LDV,ql <EVENT>s<ESZ>=<DATA>,0(,),\
             <FILE>s<FSZ>=<DATA>,1(,),\
             <OBJ>s<OSZ>=<DATA>,2(,) .
@.
@.    #  Start BisBug Debugging Session
@     LCV,lin+2 '' <FILE> 'BisBug' ,<OCC>i3 .                                   Check for BisBug
@.    BR <BISBUG> .
@.
@.    #  Start BisCmd
@     LDV <BISCMD>h1=n .
@     LCV,0037 '' <FILE> 'BisCmd' ,<OCC>i3  LDV <BISCMD>h1=y .                  Check for BisCmd
@     CALL,BISCMD_ 0001 (<STATUS>h1) .
@     IF <STATUS> eq 9,(0036) .
@.
@.    #  Make sure were on the correct site for this file...
@0037:LCV,0038 'b1' <FILE> 'site-' <POS>i3 .                                    Find string
@     INC,5 <POS>  LDV <SL>h1=<FILE>(<POS>-1) .                                 Read Site Letter from file
@     IF <SL> ne NETSIT$ . ; GTO 0038 .                                         Compare
@     LDV <MSG>s256='Site letters differ... Cannot save file.' .
@     CALL,GROWL_ 0001 ('subwatcher: warning!','FAILED',<MSG>) .
@     GTO 0036 .
@.
@0038:.
@     CALL,SUBLCOMMON_ getFileName_ (<FILE>,<RUN>s60) .                         Extracts File Name from path
@.
@.    #  Only .bis files are allowed past this point
@     LCV,0036 'b99' <RUN> '.bis'/'' .                                          Remove file type
@     LCV,lin+1 'b99' <RUN> '.html'/'' .                                        Remove file type
@     IF <PRUN> ne '<PRUN>'  IF <RUN> eq <PRUN>,(0036) .                        Catch duplicates
@     DEF,s <PSZ>i3,<RUN>  LDV <PRUN>s<PSZ>=<RUN> .
@     LDV,p <RUN> .
@.
@.    #  Load run location
@     CALL,SUBLCOMMON_ getCabDrwRpt_ (<RUN>,<CAB>i4,<DRW>h1,<RPT>i4) .
@.
@.    #  Logging
@     DFU SUBLOG_ .
@     LN+,SUBLOG_,,1  LDV <WLN>i7=STAT1$ .
@     WRL,SUBLOG_,<WLN>,Y 'Event','Report','User','Date','Time','File','Object' \
        ¬,<EVENT>,<RUN>,USER$,DATE11$,TIME$,<FILE>,<OBJ> .
@     CMU .
@.
@.ew  IF <EVENT> eq 'deleted',(0036) .                                          File deleted?
@     IF <BISCMD> eq 'y',(0036) .                                               File deleted?
@.
@.    #  Make sure its a valid MAPPER report
@     LDV <VM>i1=0 .
@     DEF <T>i2,<CAB>  IF <T> eq 1  INC <VM> .                                  Numeric
@     DEF <T>i2,<RPT>  IF <T> eq 1  INC <VM> .                                  Numeric
@     DEF <T>i2,<DRW>  IF <T> eq 2  INC <VM> .                                  Alpha
@     IF <VM> eq 3,(0047) .                                                     Valid
@     PC,bcw "del <CHGSFILE>(p)" .                                              Clean up file
@.
@     LDV <MSG>s80=<FILE>': Is not a valid MAPPER report' .
@     CALL,GROWL_ 0001 ('subwatcher: warning!','FAILED',<MSG>) .
@     GTO 0036 .
@.
@.    #  Retrieve the file and clear attribute again
@0047:PCR,0,I,-0,n,n,0034 <FILE>  GTO 0048 .
@     IF STAT1$ eq 1 \
        LDV <MSG>s80='The file has been deleted' .
@     IF STAT1$ eq 2 \
        LDV <MSG>s80='Binary files are not supported' .
@     PC,bcw "del <CHGSFILE>(p)" .                                              Clean up file
@.ew  MBX,OK,S,'Sublwatcher: Warning!' "<MSG>(p)" O,(0036) .
@     CALL,GROWL_ 0001 ('subwatcher: warning!','FAILED',<MSG>) .
@     GTO 0036 .
@.
@0048:RNM -4 .
@     PC,bcw "del <CHGSFILE>(p)" .                                              Clean up file
@.
@.    #  Make sure were not editing this file
@     IF <CAB> eq ECAB$  IF <DRW> eq AEDRW$  IF <RPT> eq ERPT$,(0036) . ; .     Cannot modify itself
@.
@.    #  Check if brand new report
@     LZR,<CAB>,<DRW>,<RPT>,lin+1  LDV <EXISTS>h1='Y'  GTO 0050 .
@     LDV <EXISTS>h1='N',<MSG>s256='Can not create brand new file.' .
@     LDV <MSG>=<MSG>'This feature isnTIC$t available yet.' .
@.ew  MBX,OK,S,'Sublwatcher: Warning!' "<MSG>(p)" O,(0036) .
@     CALL,GROWL_ 0001 ('subwatcher: warning!','FAILED',<MSG>) .
@     GTO 0036 .
@.
@.    #  Check if user matches control line
@0050:RDL,<CAB>,<DRW>,<RPT>,1 53-11 <CONTROLUSER>s .
@     IF USER$ ne <CONTROLUSER> . ; GTO 0056 .
@     LDV <MSG>s256='You do not have update control of this report.',\
          <MSG>=<MSG>(p)'Waiting for input...' .
@     CALL,GROWL_ 0001 ('subwatcher: warning!','FAILED',<MSG>) .
@     MBX,YN,Q,'Sublwatcher: Whoa there!' \
"You do not have update control of this report. //\
 Run:   <RPT>(p)<DRW>(p)<CAB>(p)    Name:   <CONTROLUSER>(p) //\
 Would you like to take control of this report?//" Y,(lin+1),N,(0036) .
@0056:LDV <REPORT>h11='1D3950' .
@     dir <REPORT> ,,,,,<ACC>h1  IF <ACC> eq '',(0061) .                         ICE Enabled Site?
@     LZR,3950,d,1,0061 .                                                       ICE Enabled Site?
@     SRH,3950,d,1,,,0061 'ad' 'SvcCab','sd'  ,<CAB>,<DRW> .
@     LZR,-0 ,,<HLN>i7  INC <HLN> .
@     RDL,-0,<HLN>,lin+1 'cab','dw','rpt' <ICAB>h,<IDW>h,<IRPT>h .
@     FDR,<ICAB>,<IDW>,<IRPT>,,,0061 '' 'cab','dw','rpt'  ,<CAB>,<DRW>,<RPT> .
@     RLN,,lin+1 'service' <SRVS>s  GTO 0061 .
@     CALL,GROWL_ 0001 ('subwatcher: warning!','FAILED','Check input message') .
@     MBX,YN,Q,'What are we doing?' \
"Do you want to add this as an ice service//" Y,(0019),N,(0061) .
@.
@.    #  Load the ¬ character and handle the conversion
@0061:.
@     RSL,-4 .
@     CALL,SUBLCOMMON_ charReplace_ ('import') .                                Replace tab character
@     RNM -4 .
@.
@     LOK,<CAB>,<DRW>,<RPT>,lin+1  GTO 0063 .
@     LDV <MSG>s80='Updating of report '<RPT>(p)<DRW>(p)<CAB>(p)' is in progress...' .
@     CALL,GROWL_ 0001 ('subwatcher: warning!','FAILED',<MSG>) .
@     GTO 0036 .
@0063:REP,-4,<CAB>,<DRW>,<RPT> .
@     ULK .
@     GTO 0036 .
@.
@.    #  Start over and keep re-checking
@0066:IF WS$ eq 0,(lin+2) .
@     IF <EXISTS> eq y  RUN V,<RPT>,<DRW>,<CAB> . ; .
@     GTO END .
@.
@.    #  Start over and keep re-checking
@0070:PCR,0,I,-0,n,n,lin+2 <CHGSFILE>(p) .                                      File Exist
@     PC,bcw "del <CHGSFILE>(p)" .                                              Clean it up
@     GTO 0010 .
@.
@.    #  Run was aborted
@0198:.
@     REL .
@.
