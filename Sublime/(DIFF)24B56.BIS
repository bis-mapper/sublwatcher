.Object Name: DIFF
*
*===================================================================================================
:INCLUDE,ECAB$,B,1 .                                                                         INCLUDE
@.
@.    #---------------------------------------------------------------------------------------------
@.    #  DIFF
@.    #  TODO Write program description
@.    #  TODO Include examples and documentations
@.    #  Written: 11/21/2013
@.    #---------------------------------------------------------------------------------------------
@.
@.    # Test Setup
@ rdb LDV <CTL>s80='DIFF' .
@     LDV <CTL>s80='DIF 24b56' .
@     LDV <FILE>s132='d:\users\winne\sublwatcher\site-e\Umbrella\Umbrella_Test\(Summary)20f832.bis' .
@     CALL 0001 (<CTL>,<FILE>,'',<STATUS>h1) .
@     GTO END .
@.
@0001:(<CTL>,<FILE>,<RUNNAME>,<STATUS>) .
@.
@     SUBLRER_ .
@.
@     RSR,SUBLCOMMON_ configVars_ .
@.
@     LCV 't@' <CTL> ' ' <pos>i3  inc <pos> .
@     LDV <PRUN>h18=<CTL>(<pos>-0) .



@.    #  Check for site flag
@     lcv,lin+4 '' <PRUN> '-s' <spos>i3 .
@     inc,2 <spos>  ldv,p <psite>s40=<PRUN>(<spos>-0),<samesite>h1='N' .
@     dec,3 <spos>  ldv,p <t>s40=<PRUN>(1-<spos>),<PRUN>s40=<t> .           Exclude site option
@     LCV '' <CTL> '-s' <spos>i3  if <pos> eq <spos> ldv <PRUN>h1=' ' .
@     if netsit$ eq <psite> ldv <samesite>h1='Y' . ; .



@.
@.    Get bis script and make sure has correct extension
@0005:CALL,SUBLCOMMON_ getFileName_ (<FILE>,<RUN>s60) .                         Extracts File Name from path
@     LDV <TFILE>s256=<FILE>  LCV '' <TFILE> <RUN>/'' .
@     LCV,lin+1 'b99' <RUN> '.bis'/''  GTO lin+4 .                                   Remove file type
@     LDV <MSG>s256='Cannot do diff on non BIS file type' .
@     CALL,GROWL_ 0001 ('Sublwatcher: Warning!','FAILED',<MSG>) .
@     GTO 0199 .
@.
@.    #  Make sure files exists
@     CALL,SUBLCOMMON_ getCabDrwRpt_ (<RUN>,<CAB>i4,<DRW>h1,<RPT>i4) .
@     PCR,0,I,-0,n,n,lin+1 <FILE>  RNM -2  GTO lin+4 .
@     LDV <MSG>s256='Could not locate '<FILE> .
@     CALL,GROWL_ 0001 ('Sublwatcher: Warning!','FAILED',<MSG>) .
@     GTO 0199 .
@.
@.    #  Get file attributes date time
@     LDV <ATTFIL>s80='c:\temp\attrib.txt' .
@     PC,bcw "dir <FILE>(p) > <ATTFIL>" .
@     PCR,0,I,-0,n,n,lin1 <ATTFIL> .
@     RDL,-0,7,lin1  1-10,13-5 <DATE>h,<TIME>h  GTO lin+2 .
@     LDV <DTIME>h0,<D2DATE>h0  GTO 0010 .
@     DC D2=D16(<DATE>) <D2DATE>h9 .
@     if time$ lt 12:00:00 <addhours>i1=0 . ; <addhours>i2=12 .
@     CHG <hrs>i2 <TIME>(1-2) + <addhours> .
@     LDV <DTIME>h8=<hrs>(z)<TIME>(3-0)':00' .
@.
@.    #  Check if run was etnered.  Otherwise just use local copy
@0010:IF <PRUN> eq '',' ' . ; GTO 0015 .

@     IF <samesite> eq 'N' gto 0012 . ; .

@     LZR,<CAB>,<DRW>,<RPT>,lin1  GTO LIN+4 .
@     LDV <MSG>s256='Could not locate '<CAB>(p)<DRW><RPT>(p) .
@     CALL,GROWL_ 0001 ('Sublwatcher: Warning!','FAILED',<MSG>) .
@     GTO 0199 .
@.
@     RDL,<CAB>,<DRW>,<RPT>,1 1-80 <INFO>s .                                       Get time and date
@     RSL,<CAB>,<DRW>,<RPT> .                                                             Get result
@     CALL,SUBLCOMMON_ charReplace_ ('export') .                               Replace tab character
@     RNM -1 .
@     DUP,-1,0,I .
@     LDV <LOCALRPT>i4=RPT$ .
@     GTO 0020 .

@0012:
@     net,,<psite>,user$,depn$ .
@     nrd,<cab>,<drw>,<rpt>,0,i,-15,lin+1  gto lin+4.
@     LDV <MSG>s256='Could not locate remote file '<RPT>(p)<DRW><CAB>(p) .
@     CALL,GROWL_ 0001 ('Sublwatcher: Warning!','FAILED',<MSG>) .
@     GTO 0199 .
@.    #--- Get HeaderLine
@     ldv,p <vinfo>h12='<info>s80',<vdata>h12='<info>' .
@     nrn "@brk,0,a   rdl,<cab>,<drw>,<rpt>,1 1-80 <vinfo>"  <neterror>s160 .
@     nrn,0099 "<vdata>" .
@     nrn,0099 "@brk" .
@     nrd,0,a,-0,0,a,-16,lin+1  gto lin+4 .     read result back to local system
@     LDV <MSG>s256='Failed to read headline from '<PRPT>(p)<PDRW><PCAB>(p) .
@     CALL,GROWL_ 0001 ('Sublwatcher: Warning!','FAILED',<MSG>) .
@     GTO 0199 .
@     nof .
@     rsl,-15 .

@     RDL,-16,2 1-80 <INFO>s .                                                     Get time and date
@     CALL,SUBLCOMMON_ charReplace_ ('export') .                               Replace tab character
@     RNM -1 .
@     DUP,-1,0,I .
@     LDV <LOCALRPT>i4=RPT$ .
@     GTO 0020 .

@.
@.    #  Check if alternate report was entered
@.    #  This will do a diff from the current report and the report entered.
@0015:CALL,SUBLCOMMON_ getCabDrwRpt_ (<PRUN>,<PCAB>i4,<PDRW>h1,<PRPT>i4) .

@     IF <samesite> eq 'N' gto 0017 . ; .

@     LZR,<PCAB>,<PDRW>,<PRPT>,lin1  GTO LIN+4 .
@     LDV <MSG>s256='Could not locate '<PRPT>(p)<PDRW><PCAB>(p) .
@     CALL,GROWL_ 0001 ('Sublwatcher: Warning!','FAILED',<MSG>) .
@     GTO 0199 .
@.
@     RDL,<PCAB>,<PDRW>,<PRPT>,1 1-80 <INFO>s .                                    Get time and date
@     RSL,<PCAB>,<PDRW>,<PRPT> .                                                          Get result
@     CALL,SUBLCOMMON_ charReplace_ ('export') .                               Replace tab character
@     RNM -1 .
@     DUP,-1,0,I .
@     LDV <LOCALRPT>i4=RPT$ .
@     GTO 0020 .

@.    #  DIFFERENT SITE DIFF!
@.    #  This will do a diff from the current report and the report entered.
@0017:
@     net,,<psite>,user$,depn$ .
@     nrd,<pcab>,<pdrw>,<prpt>,0,i,-15,lin+1  gto lin+4.
@     LDV <MSG>s256='Could not locate remote file '<PRPT>(p)<PDRW><PCAB>(p) .
@     CALL,GROWL_ 0001 ('Sublwatcher: Warning!','FAILED',<MSG>) .
@     GTO 0199 .
@.    #--- Get HeaderLine
@     ldv,p <vinfo>h12='<info>s80',<vdata>h12='<info>' .
@     nrn "@brk,0,a   rdl,<cab>,<drw>,<rpt>,1 1-80 <vinfo>"  <neterror>s160 .
@     nrn,0099 "<vdata>" .
@     nrn,0099 "@brk" .
@     nrd,0,a,-0,0,a,-16,lin+1  gto lin+4 .     read result back to local system
@     LDV <MSG>s256='Failed to read headline from '<PRPT>(p)<PDRW><PCAB>(p) .
@     CALL,GROWL_ 0001 ('Sublwatcher: Warning!','FAILED',<MSG>) .
@     GTO 0199 .
@     nof .
@     rsl,-15 .

@     RDL,-16,2 1-80 <INFO>s .                                                     Get time and date
@     CALL,SUBLCOMMON_ charReplace_ ('export') .                               Replace tab character
@     RNM -1 .
@     DUP,-1,0,I .
@     LDV <LOCALRPT>i4=RPT$ .


@.
@.    #  Temporary report to hold external file
@0020:RSL,-2 .
@     CALL,SUBLCOMMON_ charReplace_ ('export') .                               Replace tab character
@     RNM -2 .
@     DUP,-2,0,I .
@     LDV <EXTERNALRPT>i4=RPT$ .
@.
@.    #  Get diff between local and external file
@     DIF,0,I,<LOCALRPT>,0,I,<EXTERNALRPT> ' ' .
@     RNM -3 .
@.
@.    #  Update the control lines to refelct actual file dates and times
@     FDR,-3 '' 2-4 .,'DATE' ,<DLN1>i3 .
@     CHG <TLN>i3 <DLN1> + 1 .
@     FDR,-3,<TLN> '' 2-4 .,'DATE' ,<DLN2>i3 .
@     CHG <TLN>i3 <DLN2> + 1 .
@     FDR,-3,<TLN> '' 2-4 .,'DATE' ,<DLN3>i3 .
@     WRL,-3,<DLN2> 1-80 .,<INFO> .
@     WRL,-3,<DLN3> 20-8,34-4,38-1,42-9 .,<DTIME>,<RPT>,<DRW>(U),<D2DATE> ..
@.
@.    #  Clean up temp files
@     DLR,0,I,<LOCALRPT> .
@     DLR,0,I,<EXTERNALRPT> .
@.
@.    #  Start text editor
@.    #  Refer to "http://www.sublimetext.com/docs/2/osx_command_line.html" for all options
@     LDV <DIFFILE>s256=<BTEMP>(p)'\'<RUN>(p)'.bisdiff' .
@     PCW,-3,2,lin+1 <DIFFILE>(p)  GTO lin+4 .                                            Write file
@     LDV <MSG>s256='Could not write diff file' .
@     CALL,GROWL_ 0001 ('Sublwatcher: Warning!','FAILED',<MSG>) .
@     GTO 0199 .
@.
@     CALL,SUBLCOMMON_ appPath_ (<USER>,<APPPATH>s256) .
@     PC,r '"'<APPPATH>(p)'" -a' '"'<DIFFILE>(p)':'<DLN>(p)'"' .           start editor with options
@.
@     PC,bcw "del <DIFFILE>(p)" .                                                        Clean it up
@.
@.    # Exit
@0199:RETURN .
