.Object Name: BASHEXEC
*
*===================================================================================================
:INCLUDE,ecab$,B,1 .                                                                         INCLUDE
@.
@.    #---------------------------------------------------------------------------------------------
@.    #  BASHEXEC
@.    #  Written: 12/17/2014
@.    #
@.    #  @.    # Test Setup
@.    #  @     brk .
@.    #  #! /bin/bash
@.    #  cd <frontenddir>(p)
@.    #  git clone git@gitlab.galaxy.local:onlinequoting/frontend.git
@.    #  cd <extlibdir>(p)
@.    #  git clone git@gitlab.galaxy.local:onlinequoting/ice_modules.git
@.    #  git clone git@gitlab.galaxy.local:onlinequoting/defines.git
@.    #  read -p "Press enter to continue"
@.    #  @     brk .
@.    #  @     call,BASHEXEC_ 0001 (<file>,<status>h1) .
@.    #
@.    #---------------------------------------------------------------------------------------------
@.
@.    #  Read the remote origin
@     ldv <file>s256='%USERPROFILE%\sublwatcher\site-'NETSIT$'\icebuild\1f100.BIS' .
@     call,100,f,8 0050 (<file>,<repopath>s256,<status>h1)  .
@     call,100,f,8 0065 (<repopath>,<unixappdir>s256) .
@. @ rdb .  <unixappdir> <repopath>
@     brk .
#! /bin/bash
echo ""
echo "Read remote"
echo ""
cd <unixappdir>(p)
git remote -v > stdout.txt
@     brk .
@     ldv <batchfile>s256='%USERPROFILE%\sublwatcher\remote.bat' .
@     call 0001 (<batchfile>,<status>h1) .
@     if <status> eq 9  gto end . ; .
@.
@.    #  Read the stdout.txt from the output of the command above
@     ldv <stdout>s256=<repopath>'\stdout.txt' .
@     call,100,f,8 0070 (<stdout>,<status>h1) .  Read std out
@.
@. @.    # Test Setup (Git clone example)
@. @ rdb ldv,p <file>s256='%USERPROFILE%\sublwatcher\site-'netsit$'\test.sh' .
@. @     ldv,p <appname>s20=sublwatcher,<version>s16='1.0.0-beta' .
@. @     brk .
@. #! /bin/bash
@. DIRECTORY=//MACH$/AppBuilds/USER$
@. APPNAME=<appname>
@. echo "Does $DIRECTORY/$APPNAME exist?"
@. if [ ! -d "$DIRECTORY/$APPNAME" ]; then
@.   cd $DIRECTORY
@.   echo "Creating $DIRECTORY/$APPNAME"
@.   git clone git@gitlab.galaxy.local:utilities/sublwatcher.git
@. else
@.   echo "It exists!"
@. fi
@. cd $DIRECTORY/$APPNAME
@. echo "Fetching the latest changes"
@. git fetch
@. echo "Checking out $APPNAME branch: master"
@. git checkout master -q
@. @. cp -R <unixappdir>(p)/app/frontend/dist/<appname>(p)/* "//<server>(p)/assets/<appname>(p)"
@. read -p "Press enter to continue"
@. @     brk .
@. @     call 0001 (<file>,<status>h1) .
@. @     gto end .
@.
@0001:(<file>,<status>) .
@     rnm -1 .
@     ldv <status>=0 .
@.
@     SUBLRER_ .
@.
@.    #  Add line to file to remove when complete
@     call 0200 (<file>,<unixfile>s256) .
@     ln+,-1,,1,,'rm '<unixfile> .
@     ldv,p <successfile>s20='success-'msec$'.txt'
@     ln+,-1,,1,,'echo success > $USERPROFILE/sublwatcher/'<successfile> .     Output success.txt file
@.
@.    #  Check if the .sh file being passed exists?
@     pcw,-1,2,0181 <file> .
@.
@.    #  Does the bash execution file exist?
@0005:ldv <shell>s256='%USERPROFILE%\sublwatcher\shellexec.bat'
@     pcr,0,f,-0,,,lin+1 <shell>  gto 0010 .
@.
@.    #  Generate the file the first time
@     brk .
set shellfile=%1
set BASH="%LOCALAPPDATA%\programs\git\bin\sh.exe"
start "" "%BASH%" --login -i -c "%shellfile%"
@     brk .
@     pcw,-0,2,0180 <shell> gto 0010 .
@.
@.    #  Convert file to unix file path
@0010:pc,bnw '"'<shell>(p)' '<unixfile>'"' .
@.
@.    #  Wait for success.txt file
@     ldv,p <success>s80='%USERPROFILE%\sublwatcher\'<successfile> .
@     ldv <time>i3 .
@0015:pcr,0,f,-0,,,lin+1 <success>  gto 0020 .
@     inc <time>  if <time> gt 60 gto 0182 . ; .             max wait 30 seconds
@     dbg,,w,500 .
@     gto 0015 .
@.
@0020:PC,bcw "del <success>" .                                                         Clean up file
@     gto 0199 .
@.
@.
@.    #  Errors
@0180:ldv <status>=9,<msg>s256='Could not create file '<file> .
@     call,GROWL_ 0001 ('sublwatcher: ultimate fail!','FAILED',<msg>) .
@     gto 0199 .
@.
@0181:ldv <status>=9,<msg>s256='File "'<file>(p)'" could not be written.' .
@     call,GROWL_ 0001 ('sublwatcher: ultimate fail!','FAILED',<msg>) .
@     gto 0199 .
@.
@0182:ldv <status>=9,<msg>s256='Could not read stdout.txt file... Gave up after 30 seconds...' .
@     PC,bcw "del <success>" .                                                         Clean up file
@     call,GROWL_ 0001 ('sublwatcher: fail!','FAILED',<msg>) .
@     gto 0199 .
@.
@.    # Exit
@0199:return .
@.
@.    #  Convert standard file name with unix file name
@0200:(<file>,<unixfile>) .
@     ldv,p <t>s256=<file> .
@     lcv '' <t> '\'/'/' .
@     lcv '' <t> ':'/ .
@     ldv <cnt>i3=0
@0201:inc <cnt>
@     art mod(<cnt>,2); <r>i6  if <r> eq 0 ldv <c>h0 . ; ldv <c>h1=$ .
@. @     if <cnt> eq 1  if <c> eq '$'  lcv,lin1 '' <t>(1-1) '/'/ .
@     lcv,lin1 'b<cnt>' <t> '%'/<c>  gto 0201 .
@     ldv,p <unixfile>s256=<t> .
@     return .
@.
