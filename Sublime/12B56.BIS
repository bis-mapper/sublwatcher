.SUBL_EXPORT
.
.
.
*
*
*===============================================================================
:INCLUDE,ECAB$,B,1 .                                                            INCLUDE
@     ldv <run>h1=y,<category>s20='ICE_MODULES_TEST',<status>h1 .
@ rdb ldv <path>s256='\\excaliber\is\bis-files\site-E\' .
@     ldv <path>=<path>(p)'WINNE\Employee_Menu\ICE_Modules\' .
@     RSR,SUBLCOMMON_ configVars_ .
@     LDV <FOLDER>s20='ice_modules' .
@     ldv <path>=<appdir>'\'<folder> .
@.
@0001:(<category>,<status>) .
@     ldv <status>h1 .
@     chg <ns>h1 CHR$254 .
@     srh,3950,d,1,,,lin+1 'd' 'category' 	,<category>  gto lin+4 .
@     mbx,ok,E,'Cool ICE Admin - Export Category' \
      "Category (<category>(p)) does not exist."  O,(0199) ; .
@     rnm -1 .
@.
@.   **********  Initialize  **************************************************
@     srh,3950,D,1,,,0091 'dp' 2-20 '	',<category>  rnm -1 .    Category details
@     ldv,p <file>s256=<path>(p)'CTGTBL.BIS' .            Verify Category Table
@     lzr,-1 ,<cc>i4  lch,-1,,lin+1 'af' 1-<cc> '	'/<ns>  rnm -1 .
@     pcw,-1,2,0091 <file> .                                 Export CTGTBL file
@     fdr,-1 'a' 2-20 '	',<category> .
@     rln 'Cab','Dw','Rpt','SvcCab','Sd','StyCab','St','StyRpt','CtgLoc' \
      <idxcab>a,<idxdw>a,<idxrpt>i,<svccab>a,<svcdrw>a,\
      <stycab>a,<stydw>a,<styrpt>a,<ctgloc>a .
@     ldv <width>i6=80,<xcab>a4,<xdrw>a1 .
@     ldv <mserver>s18,<mslt>a1,<user>a11,<dept>a4,<pwd>a6 .
@     if <ctgloc> eq Rmt . ; gto 0012 .        Category on Remote Mapper Server
@     fdr,-0,,,lin+1 '' 23-6 '*',Width \
      rln 30-6 <width>  ldv,r <width> .                   Width of Object drawer
@     fdr,-1,,,0094 '' 23-6 '*',RmtSvr .       Get remote Server Specifications
@     rln 30-18,49-1 <mserver>,<mslt> .
@     fdr,-1,,,0011 '' 23-6 '*',UserId .            User authentication Enabled
@     rln 30-11,42-4,47-6 <user>,<dept>,<pwd> .  Get Default User Specifications
@0011:call,edrw$,224 0010 (<ctgloc>,<mserver>,<mslt>,\
      <user>,<dept>,<pwd>,<status>) .           Net Open to Remote Mapper Server
@     if <status> eq 9  gto 0091 ; .
@0012:if <ctgloc> eq Rmt . ; drw,<svccab>,<svcdrw>,0096 <width> .
@     fdr,3950,E,63,,,0095 '' 'Width' '	',<width>/r,999999 .  Get Working Drawer
@     rln 'Cab','Dw' <xcab>,<xdrw> .
@     if <xcab> eq ecab  ldv,w <xcab>=3950 ; .
@     if <ctgloc> eq Rmt  drw,<xcab>,<xdrw>,0098 ; .
@     rsl,<idxcab>,<idxdw>,<idxrpt>  rnm -1 .               Services Table in -1
@.   ***************  Completion window  **************************************
@     ldv <boxsiz>i2=42 .
@.   ldv,pu <t1>s20=<category> .
@.   WIN,<RWIN>,18,22,08,40,bla/lgra,cb '' <CMP1>i6 .
@.   FON,t,'MS Sans Serif',13,d .
@.   TXT,"Exporting from category: <t1>",\
     <CMP1>,02,05,01,45,bla/lgra,l <T40>i6 .
@.   FON,t,'MS Sans Serif',13,db .
@.   TXT," ",<CMP1>,04,05,03,<boxsiz>,bla/whi,bs .
@.   TXT,"",<CMP1>,04,05,03,<boxsiz>,bla/whi,lbs <T41>i6 .
@.   TXT,"0 % completed ",<CMP1>,05,13,01,17,blu/whi,rs <T42>i6 .
@     ldv <trpt>i6=0,<nrpt>i6=0 .
@     srh,-1 'dn@l(*)' 'StyCab' '	',@@@@ <vlines>i6 .       Nbr of Style Guides
@     inc,<vlines> <trpt> .
@     srh,-1 'dn@l(*)' 'Cab' '	',@@@@ <vlines>i6 .       Nbr of Service Reports
@     inc,<vlines> <trpt> .
@.   **********  Export Services Style Guides  ********************************
@0013:srh,-1,,,0016 'dn@l(*)' 'StyCab' '	',@@@@  rnm -2 . Get Service Style Guid
@     fnd,-2 '' 2-1 '*',= ,<ln>i6 .
@0014:inc <ln> .
@     rdl,-2,<ln>,0016 'StyCab','St','StyRpt' <stycab>a,<stydw>a,<styrpt>a .
@     ldv,p <c>a4=<stycab>,<d>a1=<stydw>,<r>a4=<styrpt>,<fileid>a9=<r><d><c> .
@     ldv,p <file>s85=<path>(p)SSTY<fileid>'.BIS' .
@     lzr,<stycab>,<stydw>,<styrpt> ,<cc>i4 .
@     lch,<stycab>,<stydw>,<styrpt>,,lin+1 'af' 1-<cc> '	'/<ns> .
@.ew pcw,-0,2,091 <file> .    Export Service Style Guide
@.   **********  Update Completion Message  ***********************************
@.   inc <nrpt> chg <pct>i3 <nrpt> *100 /<trpt> .     Update Completion Message
@.   chg <ncol>i3 <pct> *<boxsiz> /100 .
@.   TXT,"",<T41>,04,05,03,<ncol>,whi/blu,bls .
@.   TXT,"<pct> % completed ",<T42>,,,,,,rs .
@.   SHW,<CMP1> .
@     gto 0014 .
@.   **********  Export Services  *********************************************
@0016:srh,-1,,,0018 'dn@l(*)' 'Cab' '	',@@@@  rnm -2 .       Get Service Reports
@     fnd,-2 '' 2-1 '*',= ,<ln>i6 .
@0017:inc <ln> .
@     rdl,-2,<ln>,0018 'Service','Cab','Dw','Rpt','SvcTyp' \
       <service>s,<scab>a,<sdw>a,<srpt>a,<svctyp>a .
@     if <ctgloc> ne Rmt \
        lzr,<scab>,<sdw>,<srpt>,0207 ,,,,,,,,,,<frrtyp>a1 ; .    Current Service
@     if <svctyp> eq ASP . ; gto 0201 .                              ASP Object?
@     rsr 0160 .                                            Open ASP Script File
@     if <status> eq 9  gto 0017 ; .                                 Skip Object
@     gto 0202 .
@0201:if <ctgloc> eq Rmt . ; gto 0203 .
@     ldv,p <t1>a4=<scab>,<t2>a1=<sdw>,<t3>a4=<srpt> .
@     NRN,099 "@brk,0,a lzr,<t1>,<t2>,<t3> ,<A>i6,,,,,,,,,<B>a1 ." <vmsg>s80 .
@     NRN,099 "<B> <A>" <vmsg>s80 .
@     NRN,099 "@brk ." <vmsg>s80 .
@     NRD,0,A,-0,0,A,-0,0099 <vmsg>s80 .
@     rdl,-0,2 1-1,3-6 <frrtyp>i,<frwidth>i .
@     if <frwidth> eq 0  gto 0207 ; .
@     drw,<xcab>,<xdrw> <towidth>i6 .
@     if <frwidth> ne <towidth>  if <frrtyp> eq 1  gto 0097 ; .    Protected rpt
@     NRD,<scab>,<sdw>,<srpt>,<xcab>,<xdrw>,-0,0099 <vmsg>s80 .
@     gto 0202 .
@0203:rsl,<scab>,<sdw>,<srpt> .                                 Current Service
@0202:rnm -3 .                                                 Object to Export
@     ldv <ext>h8=BIS  if <svctyp> eq DYN,STA,TMP,RPT,QDF . ; \
        ldv <ext>=<svctyp> .
@     ldv,p <scab>,<srpt>,<ext>,<file>s256=<path>(p)<srpt><sdw><scab>'.'<ext> .
@     ldv,p <srvs>s40='('<service>(p)')' .
@     ldv,p <file>s256=<path>(p)<srvs>(p)<srpt><sdw><scab>'.'<ext> .
@     ldv <startln>i1=2,<binary>a1=n .
@     fdr,3950,E,44,,,0089 '' 'Obj' '	',<svctyp> .
@     rln 'Bn' <binary> .
@     if <binary> eq Y  if <frrtyp> eq 1 . ;\                Type does not match
     if <binary> eq N  if <frrtyp> eq 0 . ; gto 0088 .       Type does not match
@     if <binary> eq y  ldv <startln>=3 ; .
@     lzr,-3 ,<cc>i4  lch,-3,,lin+1 'af' 1-<cc> '	'/<ns>  rnm -3 .
@     pcw,-3,<startln>,0091 <file> .                      Export Service Report
@.   **********  Update Completion Message  ***********************************
@0207:gto 0017 .
@.   **********  Export Services Table  ***************************************
@0018:ldv,p <c>a4=<idxcab>,<d>a1=<idxdw>,<r>a4=<idxrpt>,<fileid>a9=<r><d><c> .
@     ldv,p <file>s85=<path>(p)'SVCTBL.BIS' .
@     lzr,-1 ,<cc>i4  lch,-1,,lin+1 'af' 1-<cc> '	'/<ns>  rnm -1 .
@     pcw,-1,2,0091 <file> .                              Export Services Table
@.   *******************  NORMAL RETURN  **************************************
@0019:ldv <status>=0 .
@     NOF .
@     gto 0199 .
@.   *******************  ERROR RETURN  ***************************************
@0091:ldv <status>=9 .
@     NOF .
@     ldv,w <cerr>i6=cerr$ car .                            Clear Abort Routine
@     gto 0199 .
@.   *******************  Error Messages  *************************************
@0088:ldv,pu <t1>s20=<service> .
@     MBX,YN,E,'Cool ICE Admin - Export Category' \
"Object type <svctyp> for object <t1> does not match the actual type \
of the object.//If you continue the object <t1> will be skipped.//\
Do you want to continue?" N,(0091),Y,(0207) .
@0089:ldv,pu <t1>s20=<service> .
@     srh,-1 'dpn' 'Service' '	',<service>  rnm -1 .   Remove from Service Table
@     MBX,YN,E,'Cool ICE Admin - Export Category' \
     "Object type <svctyp> is not defined in the object table.//\
If you continue the object <t1> will be skipped.//\
Do you want to continue?" N,(0091),Y,(0207) .
@0092:ldv,pu <t1>s20=<category> .
@     MBX,ok,E,'Cool ICE Admin - Export Category' \
     "Problems exporting category <t1> to file <file>." O,(0091) .
@0093:MBX,YN,Q,'Cool ICE Admin - Export Category' \
     "The path specified already contains a category \
previously exported.//Do you want to overwrite?" N,(0091),Y,(009) .
@0094:ldv,pu <t1>s20=<category> .
@     MBX,ok,E,'Cool ICE Admin - Export Category' \
"Keyword RMTSVR containing server specifications for \
remote Category <t1> is missing." O,(0091) .
@0095:ldv,pw <t1>a6=<width>,<t2>h1=tic$ .
@     MBX,ok,E,'Cool ICE Admin - Wide drawer not available' \
"Drawer with a width greater than <t1> is not available for temporary \
storage.//\
Allocate a drawer with a width of at least <t1> using the <t2>Allocate \
Work Drawer<t2> function." O,(0091) .
@0096:ldv,pu <t1>s20=<category> .
@     ldv,p <svccab>,<tdc>a5=<svcdrw><scab> .
@     MBX,ok,E,'Cool ICE Admin - Export Category' \
"Drawer <tdc> containing objects for category <t1> does not exist." O,(0091) .
@0097:ldv,puw <t1>s20=<service>,<t2>a6=<frwidth> .
@     srh,-1 'dpn' 'Service' '	',<service>  rnm -1 .   Remove from Service Table
@     MBX,YN,E,'Cool ICE Admin - Export Category' \
"Cannot open object <t1> from remote system.//A work drawer with a width of \
<t2> characters is required to transfer binary objects from this remote \
category.//\
If you continue the object <t1> will be skipped.//\
Do you want to continue?" N,(0091),Y,(0207) .
@0098:ldv <t2>h1=tic$ .
@     ldv,p <xcab>,<tdc>a5=<xdrw><xcab> .
@     MBX,ok,E,'Cool ICE Admin - Open Category' \
"Work drawer <tdc> does not exist.//\
Allocate a work drawer using <t2>Allocate Work Drawer<t2> function." O,(0091) .
@.   *******************  SERVER ERROR RETURN  ********************************
@0099:MBX,ok,E,'Cool ICE Admin - Connecting to Remote Cool ICE Server' \
     "The following message was returned from the remote Cool ICE server:-//\
<vmsg>" O,(0091) .
@.    *************  Open ASP Script File  ************************************
@0160:ldv <aspals>s40,<aspnam>s30 .
@     fdr,-1,,,lin+2 '' 'Service',23-6 '*',<service>,AspAls .  ASP Alias Keyword
@     rln 30-40 <aspals> .                                  ASP Directory Alias
@     fdr,-1,,,lin+2 '' 'Service',23-6 '*',<service>,AspNam .      ASP File Name
@     rln 30-30 <aspnam> .                                         ASP File Name
@     fdr,3950,E,55,,,lin+1 '' 'Alias' '	',<aspals> \
      rln 'Directory' <aspdir>s  gto 0161 .
@     ldv,pu <t1>s40=<aspals>,<t2>s20=<service> .
@     MBX,YN,Q,'Cool ICE Admin - Open ASP Script Object' \
"ASP server directory alias <t1> associated with service <t2> \
does not exist.//If you continue the objects will be skipped.//\
Do you want to continue?" N,(0165),Y,(0166) .
@0161:ldv,p <aspdir>,<aspnam>,<asppath>s132=<aspdir><aspnam> .
@     ret,0,F,n,n,lin+1 <asppath>  gto 0163 .
@     MBX,ok,E,'Cool ICE Admin - Open ASP Script Object' \
"ASP object <asppath> does not exist." O,(0165) .
@0163:ldv <status>a1=0 .
@     esr .
@0165:csr  gto 0091 .                                            Cancel Function
@0166:ldv <status>a1=9  esr .                                        Skip object
@0199:if <run> eq y  dsp,ecab$,aedrw$,erpt$ . ; return .
