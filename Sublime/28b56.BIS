.BASHEXEC
*
*===================================================================================================
:INCLUDE,ecab$,B,1 .                                                                         INCLUDE
@.
@.    #---------------------------------------------------------------------------------------------
@.    #  BASHEXEC
@.    #  Written: 12/17/2014
@.    #
@.    #  @.    # Test Setup
@.    #  @     brk .
@.    #  #! /bin/bash
@.    #  cd <frontenddir>(p)
@.    #  git clone git@gitlab.galaxy.local:onlinequoting/frontend.git
@.    #  cd <extlibdir>(p)
@.    #  git clone git@gitlab.galaxy.local:onlinequoting/ice_modules.git
@.    #  git clone git@gitlab.galaxy.local:onlinequoting/defines.git
@.    #  read -p "Press enter to continue"
@.    #  @     brk .
@.    #  @     call,BASHEXEC_ 0001 (<file>,<status>h1) .
@.    #
@.    #---------------------------------------------------------------------------------------------
@.
@.    # Test Setup
@     ldv,p <file>s256='%USERPROFILE%\sublwatcher\site-e\stripe\app\frontend\test.sh' .
@     brk .
#! /bin/bash
cd /d/users/winne/sublwatcher/site-e/stripe/app/frontend
git status
read -p "Press enter to continue"
@     brk .
@     call 0001 (<file>,<status>h1) .
@     gto end .
@.
@0001:(<file>,<status>) .
@     rnm -1 .
@     ldv <status>=0 .
@.
@     ERROR_ .
@.
@.    #  Add line to file to remove when complete
@     call 0200 (<file>,<unixfile>s256) .
@     ln+,-1,,1,,'rm '<unixfile> .
@     ldv,p <successfile>s20='success-'msec$'.txt'
@     ln+,-1,,1,,'echo success > $USERPROFILE/sublwatcher/'<successfile> .     Output success.txt file
@.
@.    #  Check if the .sh file being passed exists?
@     pcw,-1,2,0181 <file> .
@.
@.    #  Does the bash execution file exist?
@0005:ldv <shell>s256='%USERPROFILE%\sublwatcher\shellexec.bat'
@     pcr,0,f,-0,,,lin+1 <shell>  gto 0010 .
@.
@.    #  Generate the file the first time
@     brk .
set shellfile=%1
set BASH="%LOCALAPPDATA%\programs\git\bin\sh.exe"
start "" "%BASH%" --login -i -c "%shellfile%"
@     brk .
@     pcw,-0,2,0180 <shell> gto 0010 .
@.
@.    #  Convert file to unix file path
@0010:pc,nw '"'<shell>(p)' '<unixfile>'"' .
@.
@.    #  Wait for success.txt file
@     ldv,p <success>s80='%USERPROFILE%\sublwatcher\'<successfile> .
@     ldv <time>i3 .
@0015:pcr,0,f,-0,,,lin+1 <success>  gto 0020 .
@     inc <time>  if <time> gt 480  ldv <status> eq 9  gto 0020 . ; .             max wait 4 minutes
@     dbg,,w,500 .
@     gto 0015 .
@.
@0020:PC,bcw "del <success>" .                                                         Clean up file
@     gto 0199 .
@.
@.
@.    #  Errors
@0180:ldv <status>=9,<msg>s256='Could not create file '<file> .
@     call,GROWL_ 0001 ('sublwatcher: ultimate fail!','FAILED',<msg>) .
@     gto 0199 .
@.
@0181:ldv <status>=9,<msg>s256='File '<file>' could not be written.' .
@     call,GROWL_ 0001 ('sublwatcher: ultimate fail!','FAILED',<msg>) .
@     gto 0199 .
@.
@.    # Exit
@0199:return .
@.
@.    #  Convert standard file name with unix file name
@0200:(<file>,<unixfile>) .
@     ldv,p <t>s256=<file> .
@     lcv '' <t> '\'/'/' .
@     lcv '' <t> ':'/ .
@     ldv <cnt>i3=0
@0201:inc <cnt>
@     art mod(<cnt>,2); <r>i6  if <r> eq 0 ldv <c>h0 . ; ldv <c>h1=$ .
@. @     if <cnt> eq 1  if <c> eq '$'  lcv,lin1 '' <t>(1-1) '/'/ .
@     lcv,lin1 'b<cnt>' <t> '%'/<c>  gto 0201 .
@     ldv,p <unixfile>s256=<t> .
@     return .
@.
