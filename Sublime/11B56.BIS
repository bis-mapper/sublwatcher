.V     View & Display BIS Reports
.REV:  0.0.1
*===============================================================================
:INCLUDE,ECAB$,B,1 .
@. --------------------------------------------- . ------- D O C -------
@.
@.     --------------------------------------------------------------------
@.    |                                                                    |
@.    |  Description:                                                      |
@.    |      The VIEW script will display the script report you specify.   |
@.    |      If the script is registered in multiple departments,          |
@.    |      press F1 to display each report in succession.                |
@.    |                                                                    |
@.    |      Optionally, the registration report where the script is       |
@.    |      registered can be displayed by specifying the r-option.       |
@.    |                                                                    |
@.    |  Format:                                                           |
@.    |      VIEW RPTDRWCAB                                                |
@.    |                                                                    |
@.    |  Where:                                                            |
@.    |      RPTDRWCAB = 100f402                                           |
@.    |                                                                    |
@.    |  Example:                                                          |
@.    |      V 100f402                                                     |
@.    |                                                                    |
@.      ------------------------------------------------------------------
@.
@. --------------------------------------------- . ------- R E R -------
@.
@0001 USE rerstn=v0190,rermsg=v0191 .            . RER Variables
@     LDV,w <rerstn>i6=COORD$ .                  . Error Station Number
@     LDV,p <rermsg>s77='Unisys Business Information Server' .
@     IF SYSNAM$(1-1) eq 'M'  RER,0,F,2 0001 ; . . 2200 BIS
@     IF SYSNAM$(1-1) ne 'M'  RSR,0,E,2 0100 ; . . CORE BIS
@     IF SYSNAM$(1-1) ne 'M'  RER,0,E,2 0001 ; . . CORE BIS
@     RAR 0199 .                                 . ESC Key
@.
@. --------------------------------------------- . ----- I N P U T -----
@.
@     CHG INPUT$ <Run>h11 .                      . Script Inputs
@     LDV <USR>h11=USER$ .                       . USER
@     LDV <DPN>i4=DEPN$ .                        . DEPT #
@     LDV <SEC>h1=1 .                            . SEC DRW(g202 or g2)
@     IF <RUN> gt ' ',(lin3) .
@     LDV <RUN>h11=RPT$ADRW$CAB$ .
@     IF RPT$ eq 0  REL .                        . No Report on display
@     FDR,2,F,<DPN> '' 'USER-ID' ¬,<USR> .
@     RLN 'USERCAB' <UCAB>i4 .
@.
@.    #  Break apart <run> into useable <cab>,<drw>,<rpt>
@     call,SUBLCOMMON_ 0001 (<run>,<cab>i4,<drw>h1,<rpt>i4) .
@     DEF <T>i2,<CAB>  IF <t> eq 0  \
        LDV <CAB>i4=<UCAB>,<RUN>h11=<RPT><DRW><CAB>(p) .
@     DEF <T>i2,<DRW>  IF <t> ne 2,(0199) .
@     DEF <T>i2,<RPT>  IF <t> ne 1,(0199) .
@.
@. --------------------------------------------- . ---- A C C E S S $---
@.
@0005:CALL,"LIB$ACCESS" 0003* (*<usr>,*<dpn>,*<cab>,*<drw>,*<sec>,<sta>i1)
@     IF <STA> eq 1,2,(0199) .                   . ERROR
@. --------------------------------------------- . ---- M S T R D $ ----
@.
@0021:LZR,<CAB>,<DRW>,<RPT>,0199 ,<CC>i3 .       . Report Exist?
@     RNM,<CAB>,<DRW>,<RPT> -2 .                 . Rename It!
@     LDV <HDG>s<CC>  LCV '' <HDG> ' '/'#' .
@     BRK,<CAB>,<DRW> .
CS;FLD,2,1,3,<CC>,u,(red/bla),(red/bla);DATA,2,1,3;
<HDG>
 Script: <Run>(p)  User: <USR>  Script: BIS SCRIPT
<HDG>
@     BRK  SC,-00 q .                            . Quicky?
@     DSP,-02,,,,,3 .                            . Display Report
@.
@. --------------------------------------------- . ------- E R R -------
@.
@0199 LDV,pu <Run> .                             . No Find!
@     SC usk MSG,1,e,(whi/red),TIC$'<No finds of '<Run>'>'TIC$ .
@     RUN RUN$,<Run> .                           . Play It Again Sam!

.VARIABLE TABLE
*    Name    .Vnum.Sq.            Line  Numbers            .      Comment
*============.====.==.=====================================.====================
¬1MDNF       ¬V001¬01¬85*,105*,121,122                     ¬Script Reg Flags
¬Atype       ¬V002¬01¬109*,112*,113                        ¬Alpha type of RCR
¬Bis         ¬V003¬01¬120*,121*,122*,123,128               ¬Script Format
¬Depn        ¬V004¬01¬47*,49,50,54,60,61,61,86,152         ¬Dept number input
¬Dept        ¬V005¬01¬88*,102*,123,128                     ¬Current Dept number
¬IO          ¬V006¬01¬85*,105*,117,117,123,129             ¬I/O limits
¬Laslin      ¬V007¬01¬79*,92                               ¬Last line number
¬Line        ¬V008¬01¬82*,83,91,102*,144                   ¬Line number
¬Lines       ¬V009¬01¬85*,105*,118,118,123,129             ¬LLP Limits
¬Maxrid      ¬V010¬01¬61*,73*,95                           ¬Max RID number
¬Mode        ¬V011¬01¬109*,112*,113                        ¬Mode of RCR
¬Modes       ¬V012¬01¬85*,105*,119,119,123,129             ¬Mode access
¬Nxtlin      ¬V013¬01¬80*,82,91,92                         ¬Next line number
¬Nxtrid      ¬V014¬01¬61*,72*,78,86,88,94,95               ¬Next RID number
¬Opt         ¬V015¬01¬47*,103,107,115,152                  ¬Option (R=Disp Reg.)
¬Rid         ¬V016¬01¬85*,105*,113                         ¬RID number of RCR
¬Run         ¬V017¬01¬47*,48,55,57,67,69,82,107,128,150,151¬Run name
¬            ¬V017¬02¬152                                  ¬
¬Type        ¬V018¬01¬85*,105*,108,109,109,110,111,111     ¬Octal type
¬User        ¬V019¬01¬85*,86,105*,116,116,128              ¬User-ID
¬Var         ¬V021¬01¬108*,108                             ¬type of variable
¬Drw         ¬V022¬01¬111*,112                             ¬
¬Cab         ¬V023¬01¬111*,112                             ¬
¬rerstn      ¬V190¬01¬39*                                  ¬RER Station Number
¬rermsg      ¬V191¬01¬40*                                  ¬RER Message
